/// Arachnobaron

ACTOR IDMArachnobaron
{
	Health 750
	Radius 48
	Height 64
	Speed 15
	PainChance 64
	Mass 1200
    SeeSound "baron/sight"
    PainSound "baron/pain"
    DeathSound "arachnobaron/death"
    ActiveSound "baby/active"
    bloodcolor green
	MONSTER
	Species "Arachnotron"
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+BOSSDEATH
	+FLOORCLIP
    +MISSILEMORE
    +MISSILEEVENMORE
	+QUICKTORETALIATE
	+DONTMORPH
	Obituary "%o let an Arachnobaron blast %h."
	DamageFactor "SpiderRailgun", 0.0
	DamageFactor "SpiderBullet", 0.0
	Tag "Arachnobaron"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "Clip", 64, 20
	DropItem "Clip", 32, 20
	DropItem "ClipBox", 8, 50
	DropItem "Shell", 64, 4
	DropItem "Shell", 32, 20
	DropItem "ShellBox", 8, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketAmmo", 32, 1
	DropItem "RocketBox", 8, 5
	DropItem "Cell", 32, 20
	DropItem "Cell", 16, 20
	DropItem "Cellpack", 12, 100
	DropItem "IDMMana", 12
	DropItem "IDMMana", 12
	DropItem "IDMMaceAmmo", 12
	DropItem "IDMMaceAmmo", 12
	DropItem "IDMCatAmmo", 12
	DropItem "IDMCatAmmo", 12
	DropItem "IDMGas", 12
	DropItem "IDMGas", 12
	States
	{
	Spawn:
		ARBR AB 10 A_Look
		Loop
	See:
		ARBR A 3 A_BabyMetal
		ARBR ABBCC 3 A_Chase
		ARBR D 3 A_BabyMetal
		ARBR DEEFF 3 A_Chase
		Goto See
    Missile:
		ARBR A 1 BRIGHT A_Jump (128,"BaronAtk","BigBaronAtk")
        ARBR A 20 BRIGHT A_FaceTarget
		TNT1 A 0 A_Jump(128,"LaserAtk")
	PulseAtk:
		ARBR G 3 BRIGHT A_Custommissile ("ArachnoPulseBall", 19, 4, 0)
		ARBR H 3 BRIGHT
		ARBR H 1 BRIGHT A_SpidRefire
		Goto PulseAtk
    BaronAtk:
		ARBR P 2 BRIGHT A_FaceTarget
        ARBR P 5 BRIGHT
		ARBR Q 5 BRIGHT
        ARBR R 5 BRIGHT A_Custommissile ("IDMBaronBall", 30, 0, -5)
		Goto See+1
    BigBaronAtk:
		ARBR P 2 BRIGHT A_FaceTarget
        ARBR P 5 BRIGHT
		ARBR Q 5 BRIGHT
        ARBR R 5 BRIGHT A_Custommissile ("IDMBigBaronBall", 30, 0, -5)
		Goto See+1
	LaserAtk:
		ARBR G 3 BRIGHT A_Custommissile ("ArachnoLaserShot", 19, 4, 0)
		ARBR H 3 BRIGHT
		ARBR H 1 BRIGHT A_SpidRefire
		Goto LaserAtk
	Pain:
		ARBR A 3
		ARBR A 3 A_Pain
		Goto See
	Death:
		ARBR J 20 A_Scream
		ARBR K 7 A_NoBlocking
		ARBR LMNO 7
		ARBR O -1 A_BossDeath
		Stop
	Ice:
		"####" "#" 5 A_GenericFreezeDeath
		TNT1 A 0 A_BossDeath
		"----" A 1 A_FreezeDeathChunks
		Wait
    Raise:
		ARBR O 5
		ARBR ONMLKJ 5
		Goto See+1
	}
}