/// Arachnotron

ACTOR IDMArachnotron : Arachnotron
{
	Health 500
	Radius 48
	Height 64
	Mass 1200
	Speed 15
	PainChance 128
	Monster
	Species "Arachnotron"
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "baby/sight"
	PainSound "baby/pain"
	DeathSound "baby/death"
	ActiveSound "baby/active"
	Obituary "$OB_BABY"
	DamageFactor "SpiderRailgun", 0.0
	DamageFactor "SpiderBullet", 0.0
	Tag "Arachnotron"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "Clip", 64, 20
	DropItem "Clip", 32, 20
	DropItem "ClipBox", 8, 50
	DropItem "Shell", 64, 4
	DropItem "Shell", 32, 20
	DropItem "ShellBox", 8, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketAmmo", 32, 1
	DropItem "RocketBox", 8, 5
	DropItem "Cell", 32, 20
	DropItem "Cell", 16, 20
	DropItem "Cellpack", 8, 100
	DropItem "IDMMana", 8
	DropItem "IDMMana", 8
	DropItem "IDMMaceAmmo", 8
	DropItem "IDMMaceAmmo", 8
	DropItem "IDMCatAmmo", 8
	DropItem "IDMCatAmmo", 8
	DropItem "IDMGas", 8
	DropItem "IDMGas", 8
	States
	{
	Spawn:
		BSPI AB 10 A_Look
		Loop
	See:
		BSPI A 20
		BSPI A 3 A_BabyMetal
		BSPI ABBCC 3 A_Chase
		BSPI D 3 A_BabyMetal
		BSPI DEEFF 3 A_Chase
		Goto See+1
	Missile:
		BSPI A 20 BRIGHT A_FaceTarget
		TNT1 A 0 A_Jump(128,"LaserAtk")
	PulseAtk:
		BSPI G 4 BRIGHT A_CustomMissile("ArachnoPulseBall",19,4)
		BSPI H 4 BRIGHT
		BSPI H 1 BRIGHT A_SpidRefire
		Goto PulseAtk
	LaserAtk:
		BSPI G 4 BRIGHT A_CustomMissile("ArachnoLaserShot",19,4)
		BSPI H 4 BRIGHT
		BSPI H 1 BRIGHT A_SpidRefire
		Goto LaserAtk
	Pain:
		BSPI I 3
		BSPI I 3 A_Pain
		Goto See+1
	Death:
		TNT1 A 0 A_Jump(1,"Death.Doppel")
	Death.Massacre:
		BSPI J 20 A_Scream
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7
		BSPI P -1 A_BossDeath
		Stop
	Death.Doppel:
		BSPI J 1 A_Scream
		BSPI JJJJJJJJJ 1 A_FadeOut(0.05)
		TNT1 A 0 A_Playsound("doppel/morph2",CHAN_BODY,1,0,0.5)
		DOP2 T 2 Bright A_SetTranslucent(0.25)
		DOP2 SRQPO 2 Bright
		DOP2 NMLKJIHGFEDCB 2 Bright A_Fadein(0.05)
		TNT1 A 0 A_NoBlocking
		DOP2 A 1 A_SpawnItemEx("IDMDoppelganger",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		Stop
	Ice:
		"####" "#" 5 A_GenericFreezeDeath
		TNT1 A 0 A_BossDeath
		"----" A 1 A_FreezeDeathChunks
		Wait
    Raise:
		BSPI P 5
		BSPI ONMLKJ 5
		Goto See+1
	}
}

// Arachnotron plasma

ACTOR ArachnoPulseBall : ArachnotronPlasma replaces ArachnotronPlasma
{
	Radius 13
	Height 8
	Speed 25
	Damage (1 * random(1,40))
	RenderStyle Translucent
	+RANDOMIZE
	+SKYEXPLODE
	Seesound "weapons/plsrf"
	DeathSound "weapons/pulsex"
	DamageType "Pulse"
	Alpha 0.8
	Decal Arachnotronscorch
	States
	{
	Spawn:
		APLS AB 2 Bright
		TNT1 A 0 A_Jump(128,"SpawnLoop2")
	SpawnLoop:
		APLS AABB 1 Bright A_Weave(frandom(3,9),0,frandom(0,3),0)
		Loop
	SpawnLoop2:
		APLS AABB 1 Bright A_Weave(frandom(3,9),0,frandom(-3,0),0)
		Loop
	Death:
		APBX A 4 Bright
		APBX BCDE 3 Bright
		Stop
  }
}

ACTOR ArachnoLaserShot : MonsterLaserShot
{
	Decal Plasmascorchlower
	MissileType "ArachnoLaserTrail"
	Speed 40
	Damage (1* random(1,24))
	States
	{
	Spawn:
		APBX E 1 Bright
		Loop
	Death:
	    APBX E 2 Bright A_SetScale(1.2)
	    APBX E 2 Bright A_SetScale(0.9)
	    APBX E 2 Bright A_SetScale(0.6)
	    APBX E 2 Bright A_SetScale(0.3)
		Stop
	}
}

ACTOR ArachnoLaserTrail : MonsterLaserTrail
{
	States
	{
	Spawn:
	  TNT1 A 1
	  APBX EE 1 Bright A_FadeOut(0.2)
	  Stop
    }
}