/// Aradicator

ACTOR IDMAradicator : IDMArachnotron
{
	Tag "Aradicator"
	Obituary "%o was vaporized by an aradicator."
	Decal RedRailScorch
	DamageType "SpiderRailgun"
	+NOTARGET
	States
	{
	Spawn:
		RSPI AB 10 A_Look
		Loop
	See:
		RSPI A 20
		RSPI A 3 A_BabyMetal
		RSPI ABBCC 3 A_Chase
		RSPI D 3 A_BabyMetal
		RSPI DEEFF 3 A_Chase
		Goto See+1
	Missile:
		TNT1 A 0 A_Jump(128,"LaserAtk")
		RSPI A 0 A_PlaySound ("baby/chargerailgun")
		RSPI AAAA 4 A_FaceTarget
		RSPI G 4 Bright A_FaceTarget
		RSPI G 8 Bright A_FaceTarget
		TNT1 A 0 A_PlaySound ("weapons/railgun")
		RSPI H 8 Bright A_CustomRailgun(100,4,AA0000,FFFFFF,0,0,0,"IDMBulletPuff",0,0,0,0,1.0,1.0,None,-17)
		RSPI G 8 Bright
		Goto See+1
	LaserAtk:
		RSPI A 20 BRIGHT A_FaceTarget
		RSPI G 4 BRIGHT A_CustomMissile("MonsterLaserShot",19,4)
		RSPI H 4 BRIGHT A_CustomMissile("MonsterLaserShot",19,4)
		RSPI H 0 BRIGHT A_SpidRefire
		Goto LaserAtk+1
	Pain:
		RSPI I 3
		RSPI I 3 A_Pain
		Goto See+1
	Death:
		RSPI J 20 A_Scream
		RSPI K 7 A_Fall
		RSPI LMNO 7
		RSPI P -1 A_BossDeath
		Stop
	Ice:
		"####" "#" 5 A_GenericFreezeDeath
		TNT1 A 0 A_BossDeath
		"----" A 1 A_FreezeDeathChunks
		Wait
    Raise:
		RSPI P 5
		RSPI ONMLKJ 5
		Goto See+1
	}
}