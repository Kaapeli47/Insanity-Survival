ACTOR IDMCentroid
{
	Health 500
	Radius 40
	Height 40
	Mass 1000
	Speed 15
	PainChance 20
	MaxStepHeight 16
	Scale 1.25
	MONSTER
	Species "Arachnotron"
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+FLOORCLIP
	+NODROPOFF
	+BOSSDEATH
	DamageFactor "SpiderRailgun", 0.0
	DamageFactor "SpiderBullet", 0.0
	SeeSound "centroid/sight"
	PainSound "centroid/pain"
	DeathSound "centroid/death"
	ActiveSound "centroid/active"
	MeleeSound "imp/melee"
	Tag "Centroid"
	Obituary "%o was nailed by a centroid."
	HitObituary "%o was stung by a centroid."
	MeleeDamage 10
	MeleeRange 72
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "Clip", 64, 20
	DropItem "Clip", 32, 20
	DropItem "ClipBox", 8, 50
	DropItem "Shell", 64, 4
	DropItem "Shell", 32, 20
	DropItem "ShellBox", 8, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketAmmo", 32, 1
	DropItem "RocketBox", 8, 5
	DropItem "Cell", 32, 20
	DropItem "Cell", 16, 20
	DropItem "Cellpack", 8, 100
	DropItem "IDMMana", 8
	DropItem "IDMMana", 8
	DropItem "IDMMaceAmmo", 8
	DropItem "IDMMaceAmmo", 8
	DropItem "IDMCatAmmo", 8
	DropItem "IDMCatAmmo", 8
	DropItem "IDMGas", 8
	DropItem "IDMGas", 8
	States
	{
	Spawn:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		SCRP AB 10 A_Look
		Loop
	See:
		TNT1 A 0 A_PlaySound("centroid/walk",CHAN_BODY,1.0,1)
		SCRP AABB 3 A_Chase
		TNT1 A 0 A_Jump(64,"FastSee")
	See2:
		SCRP CCDD 3 A_Chase
		TNT1 A 0 A_Jump(64,"FastSee2")
		Goto See
	FastSee:
		TNT1 A 0 A_FastChase
		Goto See2
	FastSee2:
		TNT1 A 0 A_FastChase
		Goto See
	Missile:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		SCRP E 12 A_FaceTarget
		TNT1 A 0 A_PlaySound("weapons/nailstart")
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("MonsterNail", 12, -30)
		SCRP E 8 Bright A_CustomMissile("MonsterNail", 12, 30)
		TNT1 A 0 A_Jump(128,"See")
	MissileLoop:
		SCRP E 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("weapons/nailfire")
		SCRP E 4 Bright A_CustomMissile("MonsterNail", 12, -30)
		TNT1 A 0 A_PlaySound("weapons/nailfire")
		SCRP E 4 Bright A_CustomMissile("MonsterNail", 12, 30)
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		SCRP E 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("weapons/nailfire")
		SCRP E 4 Bright A_CustomMissile("MonsterNail", 12, -30)
		TNT1 A 0 A_PlaySound("weapons/nailfire")
		SCRP E 4 Bright A_CustomMissile("MonsterNail", 12, 30)
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		TNT1 A 0 A_MonsterRefire(10,"MissileEnd")
		Goto MissileLoop
	MissileEnd:
		SCRP E 12 A_Playsound("weapons/nailstop")
		Goto See
	Melee:
		SCRP E 5 A_FaceTarget
		SCRP F 5 A_PlaySound("centroid/swing",CHAN_BODY)
		SCRP G 5 A_FaceTarget
		SCRP H 5 A_MeleeAttack
		Goto See
	Pain:
		SCRP I 2 A_StopSound(CHAN_BODY)
		SCRP I 6 A_Pain
		Goto See
	Death:
		SCRP J 8 A_StopSound(CHAN_BODY)
		SCRP K 8 A_Scream
		SCRP L 6
		SCRP M 6 A_NoBlocking
		SCRP N -1 A_BossDeath
		Stop
	Raise:
		SCRP NM 8
		SCRP LKJ 6
		Goto See
	}
}