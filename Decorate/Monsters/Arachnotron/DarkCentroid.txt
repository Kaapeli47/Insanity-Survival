ACTOR IDMDarkCentroid : IDMCentroid
{
	Tag "Dark Centroid"
	Obituary "%o was paralyzed by a dark centroid."
	HitObituary "%o was tazed by a dark centroid."
	MeleeDamage 15
	States
	{
	Spawn:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		DSCR AB 10 A_Look
		Loop
	See:
		TNT1 A 0 A_PlaySound("centroid/walk",CHAN_BODY,1.0,1)
		DSCR AABB 3 A_Chase
		TNT1 A 0 A_Jump(64,"FastSee")
	See2:
		DSCR CCDD 3 A_Chase
		TNT1 A 0 A_Jump(64,"FastSee2")
		Goto See
	FastSee:
		TNT1 A 0 A_FastChase
		Goto See2
	FastSee2:
		TNT1 A 0 A_FastChase
		Goto See
	Missile:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		DSCR E 20 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("InsanityPlasmaBall", 12, -30)
		DSCR E 8 Bright A_CustomMissile("InsanityPlasmaBall", 12, 30)
		TNT1 A 0 A_Jump(128,"See")
	MissileLoop:
		DSCR E 0 A_FaceTarget
		DSCR E 6 Bright A_CustomMissile("InsanityPlasmaBall", 12, -30)
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		DSCR E 0 A_FaceTarget
		DSCR E 6 Bright A_CustomMissile("InsanityPlasmaBall", 12, 30)
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		DSCR E 0 A_FaceTarget
		DSCR E 6 Bright A_CustomMissile("InsanityPlasmaBall", 12, -30)
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		DSCR E 0 A_FaceTarget
		DSCR E 6 Bright A_CustomMissile("InsanityPlasmaBall", 12, 30)
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		TNT1 A 0 A_MonsterRefire(10,"MissileEnd")
		Goto MissileLoop
	MissileEnd:
		DSCR E 12
		Goto See
	Melee:
		DSCR E 5 A_FaceTarget
		DSCR F 5 A_PlaySound("centroid/swing",CHAN_BODY)
		DSCR G 5 A_FaceTarget
		DSCR H 5 A_MeleeAttack
		Goto See
	Pain:
		DSCR I 2 A_StopSound(CHAN_BODY)
		DSCR I 6 A_Pain
		Goto See
	Death:
		DSCR J 8 A_StopSound(CHAN_BODY)
		DSCR K 8 A_Scream
		DSCR L 6
		DSCR M 6 A_NoBlocking
		DSCR N -1 A_BossDeath
		Stop
	Raise:
		DSCR NM 8
		DSCR LKJ 6
		Goto See
	}
}