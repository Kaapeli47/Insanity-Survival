ACTOR IDMShalrath
{
	Health 750
	Mass 2000
	Radius 32
	Height 64
	Speed 10
	PainChance 20
	Monster
	Species "Arachnotron"
	+MISSILEMORE
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "vore/sight"
	PainSound "vore/pain"
	DeathSound "vore/death"
	ActiveSound "vore/active"
	Tag "Shalrath"
	Obituary "%o was tracked down by a shalrath's void sphere."
	DamageFactor "SpiderRailgun", 0.0
	DamageFactor "SpiderBullet", 0.0
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "Clip", 64, 20
	DropItem "Clip", 32, 20
	DropItem "ClipBox", 8, 50
	DropItem "Shell", 64, 4
	DropItem "Shell", 32, 20
	DropItem "ShellBox", 8, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketAmmo", 32, 1
	DropItem "RocketBox", 8, 5
	DropItem "Cell", 32, 20
	DropItem "Cell", 16, 20
	DropItem "Cellpack", 8, 100
	DropItem "IDMMana", 8
	DropItem "IDMMana", 8
	DropItem "IDMMaceAmmo", 8
	DropItem "IDMMaceAmmo", 8
	DropItem "IDMCatAmmo", 8
	DropItem "IDMCatAmmo", 8
	DropItem "IDMGas", 8
	DropItem "IDMGas", 8
	States
	{
	Spawn:
		VORE AB 10 A_Look
		Loop
	See:
		VORE AABBCCDDEEFF 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Playsound("vore/attack")
		VORE A 4 A_FaceTarget
		VORE GGHHHII 4 Bright A_FaceTarget
		VORE J 8 Bright A_CustomMissile("IDMVoreBall")
		Goto See
	Pain:
		VORE K 2
		VORE K 4 A_Pain
		Goto See
	Death:
		VORE L 7
		VORE M 7 A_Scream
		VORE N 7 A_NoBlocking
		VORE OP 7
		VORE Q -1
	Raise:
		VORE PONML 5
		Goto See
	}
}

ACTOR IDMVoreBall
{
	Damage 12
	Speed 12
	Projectile
	SeeSound "vore/missile"
	DeathSound "vore/misexpl"
	+SEEKERMISSILE
	States
	{
	Spawn:
		VOBA AAAAAABBB 1 Bright A_SpawnItemEx("IDMVoreTrail",random(-4,4),random(-4,4),random(-4,4),random(-1,1),random(-1,1),random(-1,1))
		TNT1 A 0 A_SeekerMissile(360,360,SMF_LOOK|SMF_PRECISE)
		VOBA BBBCCCCCC 1 Bright A_SpawnItemEx("IDMVoreTrail",random(-4,4),random(-4,4),random(-4,4),random(-1,1),random(-1,1),random(-1,1))
		TNT1 A 0 A_SeekerMissile(360,360,SMF_LOOK|SMF_PRECISE)
		Loop
	Death:
		TNT1 A 0 A_Explode(96,64,0)
		VOBA D 4 Bright A_Scream
		VOBA EFGHI 4 Bright
		Stop
	}
}

ACTOR IDMVoreTrail
{
	Radius 3
	Height 3
	RenderStyle Add
	Alpha 0.67
	+NOGRAVITY
	+NOBLOCKMAP
	+DONTSPLASH
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 3
		VOBT ABCDE 3 Bright
		Stop
	}
}