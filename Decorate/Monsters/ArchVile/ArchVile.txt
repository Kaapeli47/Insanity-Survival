/// Arch Vile

ACTOR IDMArchvile : Archvile
{
	Health 700
	Radius 20
	Height 56
	Mass 1000
	Speed 15
	PainChance 10
	Monster
	MaxTargetRange 896
	Species "Archvile"
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+QUICKTORETALIATE
	+FLOORCLIP
	+NOTARGET
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/stop"
	Obituary "$OB_VILE"
	Tag "Archvile"
	DropItem "IDMHealthBonusPickup", 128
	DropItem "IDMArmorBonusPickup", 128
	DropItem "Clip", 64, 20
	DropItem "Clip", 64, 20
	DropItem "ClipBox", 32, 50
	DropItem "Shell", 64, 4
	DropItem "Shell", 64, 4
	DropItem "ShellBox", 32, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketBox", 32, 5
	DropItem "Cell", 64, 20
	DropItem "Cell", 64, 20
	DropItem "Cellpack", 32, 100
	DropItem "IDMMana", 32
	DropItem "IDMMana", 32
	DropItem "IDMMaceAmmo", 32
	DropItem "IDMMaceAmmo", 32
	DropItem "IDMCatAmmo", 32
	DropItem "IDMCatAmmo", 32
	DropItem "IDMGas", 32
	DropItem "IDMGas", 32
	States
	{
	Spawn:
		VILE AB 10 A_Look
		Loop
	See:
		VILE AABBCCDDEEFF 2 A_VileChase
		Loop
	Missile:
		TNT1 A 0 A_VileStart
		TNT1 A 0 A_Jump(128,"ProjAtk")
		VILE G 10 BRIGHT A_FaceTarget
		VILE H 8 BRIGHT A_VileTarget
		VILE IJKLMN 8 BRIGHT A_FaceTarget
		VILE O 8 BRIGHT A_VileAttack("vile/stop",20,70,70,1.0,"Fire",1)
		VILE P 10 BRIGHT
		VILE P 10
		Goto See
	ProjAtk:
		TNT1 A 0 A_ChangeFlag(NORADIUSDMG,true)
		VILE G 4 BRIGHT A_FaceTarget
		VILE H 4 BRIGHT
		VILE IJKLMN 4 BRIGHT A_FaceTarget
		TNT1 A 0 A_CustomMissile("IDMArchMissile",28,0,-5,CMF_AIMDIRECTION)
		TNT1 A 0 A_CustomMissile("IDMArchMissile",28,0,5,CMF_AIMDIRECTION)
		VILE O 4 BRIGHT A_CustomMissile("IDMArchMissile",28,0,0,CMF_AIMDIRECTION)
		VILE P 4 BRIGHT
		TNT1 A 0 A_ChangeFlag(NORADIUSDMG,false)
		Goto See
	Heal:
		VILE "[\]" 10 BRIGHT
		Goto See
	Pain:
		VILE Q 5
		VILE Q 5 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_Jump(1,"Death.Doppel")
	Death.Massacre:
		VILE Q 7
		VILE R 7 A_Scream
		VILE S 7 A_NoBlocking
		VILE TUVWXY 7
		VILE Z -1
		Stop
	Death.Doppel:
		VILE Q 1 A_Scream
		VILE QQQQQQQQQ 1 A_FadeOut(0.05)
		TNT1 A 0 A_Playsound("doppel/morph2",CHAN_BODY,1,0,0.5)
		DOP2 T 2 Bright A_SetTranslucent(0.25)
		DOP2 SRQPO 2 Bright
		DOP2 NMLKJIHGFEDCB 2 Bright A_FadeIn(0.05)
		TNT1 A 0 A_NoBlocking
		DOP2 A 1 A_SpawnItemEx("IDMDoppelganger",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR IDMArchvileFire : ArchvileFire replaces ArchvileFire
{
  +NOBLOCKMAP
  +NOGRAVITY
  RenderStyle Add
  Alpha 0.9
  DamageType "Fire"
  States
  {
  Spawn:
    FIRE A 2 Bright A_StartFire
    FIRE BAB 2 Bright A_Fire
    FIRE C 2 Bright A_FireCrackle
    FIRE BCBCDCDCDEDED 2 Bright A_Fire
    FIRE E 2 Bright A_FireCrackle
    FIRE FEFEFGHGHGH 2 Bright A_Fire
    Stop
  }
}

ACTOR IDMArchMissile
{
	Damage 0
	Speed 50
	Projectile
	Seesound "MageStaffFire"
	Reactiontime 70
	+FLOORHUGGER
	+RIPPER
	States
	{
	Spawn:
	  TNT1 A 1
	SpawnLoop:
	  TNT1 A 0 A_SpawnItem("IDMArchvileFastFire")
	  TNT1 A 1 A_CountDown
	  Loop
	Death:
	  TNT1 A -1
	  Stop
	}
}

ACTOR IDMArchVileFastFire : IDMArchvileFire
{
  Scale 0.65
  Alpha 0.75
  DamageType "Fire"
  States
  {
  Spawn:
	TNT1 A 0 A_FireCrackle
  Spawn1:
    FIRE A 2 Bright A_Explode(6,32,0)
    FIRE B 2 Bright
	TNT1 A 0 A_FireCrackle
	TNT1 A 0 A_Jump(128,"Spawn1")
  Spawn2:
    FIRE C 2 Bright A_Explode(6,32,0)
    FIRE D 2 Bright
	TNT1 A 0 A_FireCrackle
	TNT1 A 0 A_Jump(128,"Spawn2")
  Spawn3:
    FIRE E 2 Bright A_Explode(6,32,0)
    FIRE F 2 Bright
	TNT1 A 0 A_Jump(128,"Spawn3")
    Stop
	}
}

ACTOR IDMStealthArchvile : IDMArchvile
{
	+STEALTH
	Tag "Stealth Vile"
	Obituary "%o was invisibly incinerated by a stealthvile."
	States
	{
	Death:
		VILE Q 7
		VILE R 7 A_Scream
		VILE S 7 A_NoBlocking
		VILE TUVWXY 7
		VILE Z -1
		Stop
	}
}