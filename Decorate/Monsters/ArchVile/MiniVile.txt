ACTOR IDMMinivile : IDMArchvile
{
	Health 350
	Scale 0.5
	Radius 10
	Height 28
	MinMissileChance 100
	Obituary "%o got tiny burns from a minivile."
	Species "Archvile"
	SeeSound "minivile/sight"
	PainSound "minivile/pain"
	DeathSound "minivile/death"
	ActiveSound "minivile/active"
	MeleeSound "minivile/stop"
	Tag "Minivile"
	States
	{
	See:
		VILE AABBCCDDEEFF 1 A_VileChase
		Loop
	Missile:
		TNT1 A 0 A_Playsound("minivile/attack",CHAN_VOICE)
		TNT1 A 0 A_Jump(128,"ProjAtk")
		VILE G 5 BRIGHT A_FaceTarget
		VILE H 5 BRIGHT A_VileTarget("IDMMiniVileFire")
		VILE IJKLMN 5 BRIGHT A_FaceTarget
		VILE O 5 BRIGHT A_VileAttack("vile/stop",10,35,70,0.5,"Fire",1)
		VILE P 10 BRIGHT
		Goto See
	ProjAtk:
		TNT1 A 0 A_ChangeFlag(NORADIUSDMG,true)
		VILE G 3 BRIGHT A_FaceTarget
		VILE H 3 BRIGHT
		VILE IJKLMN 3 BRIGHT A_FaceTarget
		TNT1 A 0 A_CustomMissile("IDMMiniArchMissile",28,0,-5,CMF_AIMDIRECTION)
		TNT1 A 0 A_CustomMissile("IDMMiniArchMissile",28,0,5,CMF_AIMDIRECTION)
		VILE O 3 BRIGHT A_CustomMissile("IDMMiniArchMissile",28,0,0,CMF_AIMDIRECTION)
		VILE P 3 BRIGHT
		TNT1 A 0 A_ChangeFlag(NORADIUSDMG,false)
		Goto See
	Death:
		VILE Q 5
		VILE R 5 A_Scream
		VILE S 5 A_NoBlocking
		VILE TUVWXY 5
		VILE Z -1
		Stop
	}
}

ACTOR IDMMinivileFire : IDMArchvileFire
{
  +NOBLOCKMAP
  +NOGRAVITY
  RenderStyle Add
  Alpha 1
  Scale 0.5
  States
  {
  Spawn:
    FIRE A 1 Bright A_StartFire
    FIRE BAB 1 Bright A_Fire
    FIRE C 1 Bright A_FireCrackle
    FIRE BCBCDCDCDEDED 1 Bright A_Fire
    FIRE E 1 Bright A_FireCrackle
    FIRE FEFEFGHGHGH 1 Bright A_Fire
    Stop
  }
}

ACTOR IDMMiniArchMissile : IDMArchMissile
{
	Speed 25
	States
	{
	Spawn:
	  TNT1 A 1
	SpawnLoop:
	  TNT1 A 0 A_SpawnItem("IDMMinivileFastFire")
	  TNT1 A 1 A_CountDown
	  Loop
	Death:
	  TNT1 A -1
	  Stop
	}
}

ACTOR IDMMiniVileFastFire : IDMArchvileFastFire
{
  Scale 0.325
  Alpha 0.75
  States
  {
  Spawn:
	TNT1 A 0 A_FireCrackle
  Spawn1:
    FIRE A 2 Bright A_Explode(1,16)
    FIRE B 2 Bright A_Explode(1,16)
	TNT1 A 0 A_FireCrackle
	TNT1 A 0 A_Jump(128,"Spawn1")
  Spawn2:
    FIRE C 2 Bright A_Explode(1,16)
    FIRE D 2 Bright A_Explode(1,16)
	TNT1 A 0 A_FireCrackle
	TNT1 A 0 A_Jump(128,"Spawn2")
  Spawn3:
    FIRE E 2 Bright A_Explode(1,16)
    FIRE F 2 Bright A_Explode(1,16)
	TNT1 A 0 A_Jump(128,"Spawn3")
    Stop
	}
}