/// Archon of Hell

Actor IDMArchonOfHell : IDMBaronOfHell
{
  Health 2000
  Radius 24
  Height 64
  Mass 3000
  Speed 8
  Scale 1.2
  PainChance 10
  Monster
  MeleeDamage 16
  Obituary "%o was no match for an archon of hell."
  HitObituary "%o was ripped apart by an archon of hell."
  BloodColor "20 60 20"
  DamageFactor "CybruiserBullet", 0.0
  DamageFactor "Rocket", 0.75
  DamageFactor "Grenade", 0.75
  DamageFactor "Fire", 0.75
  DamageFactor "GreenFire", 0.75
  DamageFactor "PlayerRocket", 0.75
  DamageFactor "PlayerGrenade", 0.75
  Species "Bruiser"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +FLOORCLIP
  +MISSILEMORE
  +MISSILEEVENMORE
  +BOSS
  SeeSound "monster/ar2sit"
  PainSound "baron/pain"
  DeathSound "monster/ar2dth"
  MeleeSound "baron/melee"
  SpawnID 174
  Tag "Archon of Hell"
  DropItem "IDMHealthBonusPickup", 128
  DropItem "IDMArmorBonusPickup", 128
  DropItem "IDMHealthBonusPickup", 128
  DropItem "IDMArmorBonusPickup", 128
  DropItem "Clip", 64, 20
  DropItem "Clip", 64, 20
  DropItem "ClipBox", 32, 50
  DropItem "Shell", 64, 4
  DropItem "Shell", 64, 20
  DropItem "ShellBox", 32, 10
  DropItem "RocketAmmo", 64, 1
  DropItem "RocketAmmo", 64, 1
  DropItem "RocketBox", 32, 5
  DropItem "Cell", 64, 20
  DropItem "Cell", 64, 20
  DropItem "Cellpack", 16, 100
  DropItem "IDMMana", 16
  DropItem "IDMMana", 16
  DropItem "IDMMaceAmmo", 16
  DropItem "IDMMaceAmmo", 16
  DropItem "IDMCatAmmo", 16
  DropItem "IDMCatAmmo", 16
  DropItem "IDMGas", 16
  DropItem "IDMGas", 16
  States
  {
  Spawn:
    BOS4 AB 10 A_Look
    Loop
  See:
    BOS4 AABBCCDD 3 A_Chase
    Loop
  Missile:
    BOS4 B 0 A_Jump(128,15)
    BOS4 EF 8 Bright A_FaceTarget
    BOS4 G 0 Bright A_CustomMissile("IDMBaronBall",28,0,0,0)
    BOS4 G 0 Bright A_CustomMissile("IDMBaronBall",28,0,-4,0)
    BOS4 G 3 Bright A_CustomMissile("IDMBaronBall",28,0,4,0)
    BOS4 H 3 Bright
    BOS4 H 0 A_Jump(64,8)
    BOS4 IJ 8 Bright A_FaceTarget
    BOS4 K 0 Bright A_CustomMissile("IDMBaronBall",28,0,0,0)
    BOS4 K 0 Bright A_CustomMissile("IDMBaronBall",28,0,-4,0)
    BOS4 K 3 Bright A_CustomMissile("IDMBaronBall",28,0,4,0)
    BOS4 L 3 Bright
    BOS4 L 0 A_Jump(64,1)
    Goto See
    BOS4 MN 8 Bright A_FaceTarget
    BOS4 O 0 Bright A_Jump(128,3)
    BOS4 O 4 Bright A_CustomMissile("IDMArchonComet",28,0,0,0)
    BOS4 P 4 Bright
    Goto See
    BOS4 O 0 A_CustomMissile("IDMBaronBall",28,0,-6,0)
    BOS4 O 0 A_CustomMissile("IDMBaronBall",28,0,-4,0)
    BOS4 O 0 A_CustomMissile("IDMBaronBall",28,0,-2,0)
    BOS4 O 0 A_CustomMissile("IDMBaronBall",28,0,2,0)
    BOS4 O 0 A_CustomMissile("IDMBaronBall",28,0,4,0)
    BOS4 O 3 Bright A_CustomMissile("IDMBaronBall",28,0,6,0)
    BOS4 P 3 Bright
    Goto See
  Melee:
    BOS4 EF 8 Bright A_FaceTarget
    BOS4 G 3 Bright A_MeleeAttack
    BOS4 H 3 Bright
    BOS4 H 0 A_Jump(128,1)
    Goto See
    BOS4 IJ 8 Bright A_FaceTarget
    BOS4 K 3 Bright A_MeleeAttack
    BOS4 L 3 Bright
    BOS4 L 0 A_Jump(64,1)
    Goto See
    BOS4 MN 8 Bright A_FaceTarget
    BOS4 O 0 Bright A_MeleeAttack
    BOS4 O 3 Bright A_MeleeAttack
    BOS4 P 3 Bright
    Goto See
  Pain:
    BOS4 Q 2
    BOS4 Q 2 A_Pain
    Goto See
  Death:
  Death.Massacre:
    BOS4 R 6 Bright
    BOS4 S 6 Bright A_Scream
    BOS4 T 6 Bright
    BOS4 U 6 Bright A_NoBlocking
    BOS4 VW 6 Bright
    BOS4 X 6 Bright A_CustomMissile("IDMArchonSoul",86,0,0,2,90)
    BOS4 Y 6 Bright
    BOS4 Z 6 Bright A_NoBlocking
    BOS4 [ 6 Bright A_BossDeath
    Stop
  }
}

Actor IDMArchonComet
{
  Radius 8
  Height 12
  Speed 25
  Damage 20
  Scale 1.0
  SpawnID 251
  Projectile
  +ThruGhost
  SeeSound "weapons/firbfi"
  DeathSound "weapons/hellex"
  DontHurtShooter
	DamageType "GreenFire"
  States
  {
  Spawn:
    ARCB AAAABBBBCCCC 1 Bright A_SpawnItemEx("IDMArchonCometTrail",0,0,0,0,0,0,0,128)
    Loop
  Death:
    ARCB J 0 Bright A_SetTranslucent (0.67,1)
    ARCB J 3 Bright
    ARCB K 3 Bright A_Explode(128,128,0)
    ARCB LMN 3 Bright
    Stop
  }
}

Actor IDMArchonSoul
{
  Radius 1
  Height 1
  Speed 3
  Projectile
  RenderStyle Add
  Alpha 0.80
  States
  {
  Spawn:
    BFX1 ABCD 6 Bright
    Stop
  }
}

Actor IDMArchonCometTrail
{
  Radius 3
  Height 3
  Scale 0.75
  Speed 0
  Projectile
  RenderStyle Add
  Alpha 0.67
  States
  {
  Spawn:
    TNT1 A 3 Bright
    ARCB DEFGHI 3 Bright
    Stop
  }
}