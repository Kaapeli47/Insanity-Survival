/// Baron of Hell

ACTOR IDMBaronOfHell : BaronOfHell
{
	Health 1000
	Radius 24
	Height 64
	Mass 2000
	Speed 8
	PainChance 50
	Monster
	Species "Bruiser"
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "baron/sight"
	PainSound "baron/pain"
	DeathSound "baron/death"
	ActiveSound "baron/active"
	Obituary "$OB_BARON"
	HitObituary "$OB_BARONHIT"
	BloodColor "Green"
	DamageFactor "CybruiserBullet", 0.0
	Tag "Baron of Hell"
	DropItem "IDMHealthBonusPickup", 128
	DropItem "IDMArmorBonusPickup", 128
	DropItem "Clip", 64, 20
	DropItem "Clip", 48, 20
	DropItem "ClipBox", 16, 50
	DropItem "Shell", 64, 4
	DropItem "Shell", 48, 20
	DropItem "ShellBox", 16, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketAmmo", 48, 1
	DropItem "RocketBox", 16, 5
	DropItem "Cell", 32, 20
	DropItem "Cell", 24, 20
	DropItem "Cellpack", 12, 100
	DropItem "IDMMana", 12
	DropItem "IDMMana", 12
	DropItem "IDMMaceAmmo", 12
	DropItem "IDMMaceAmmo", 12
	DropItem "IDMCatAmmo", 12
	DropItem "IDMCatAmmo", 12
	DropItem "IDMGas", 12
	DropItem "IDMGas", 12
	States
	{
	Spawn:
		BOSS AB 10 A_Look 
		Loop
	See:
		BOSS AABBCCDD 3 A_Chase
		Loop
	Missile:
		BOSS EF 8 A_FaceTarget
		TNT1 A 0 A_Jump(64,"ScatterAtk","BigScatterAtk")
		TNT1 A 0 A_Jump(128,"BigAtk")
		BOSS G 8 A_CustomComboAttack("IDMBaronBall", 32, 10 * random(1, 8), "baron/melee")
		Goto See
	Melee:
		BOSS EF 8 A_FaceTarget
		TNT1 A 0 A_Jump(128,"BigAtk")
		BOSS G 8 A_CustomComboAttack("IDMBaronBall", 32, 10 * random(1, 8), "baron/melee")
		Goto See
	BigAtk:
		BOSS G 8 A_CustomComboAttack("IDMBigBaronBall", 32, 10 * random(1, 8), "baron/melee")
		Goto See
	/*
	BurstAtk:
		BOSS G 3 A_CustomComboAttack("IDMBaronBall", 32, 10 * random(1, 8), "baron/melee")
		BOSS G 3 A_CustomMissile("IDMBaronBall", 32, 0)
		BOSS G 2 A_CustomMissile("IDMBaronBall", 32, 0)
		Goto See
	*/
	ScatterAtk:
		TNT1 A 0 A_CustomMissile("IDMBaronBall",32,0,-6)
		TNT1 A 0 A_CustomMissile("IDMBaronBall",32,0,6)
		BOSS G 8 A_CustomMissile("IDMBaronBall",32)
		Goto See
	BigScatterAtk:
		TNT1 A 0 A_CustomMissile("IDMBigBaronBall",32,0,-6)
		TNT1 A 0 A_CustomMissile("IDMBigBaronBall",32,0,6)
		BOSS G 8 A_CustomMissile("IDMBaronBall",32)
		Goto See
	Pain:
		BOSS H 2
		BOSS H 2 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_Jump(1,"Death.Doppel")
	Death.Massacre:
		BOSS I 8
		BOSS J 8 A_Scream
		BOSS K 8
		BOSS L 8 A_NoBlocking
		BOSS MN 8
		BOSS O -1 A_BossDeath   
		Stop
	Death.Doppel:
		BOSS I 1 A_Scream
		BOSS IIIIIIIII 1 A_FadeOut(0.05)
		TNT1 A 0 A_Playsound("doppel/morph2",CHAN_BODY,1,0,0.5)
		DOP2 T 2 Bright A_SetTranslucent(0.25)
		DOP2 SRQPO 2 Bright
		DOP2 NMLKJIHGFEDCB 2 Bright A_Fadein(0.05)
		TNT1 A 0 A_NoBlocking
		DOP2 A 1 A_SpawnItemEx("IDMDoppelganger",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		Stop
	Raise:
		BOSS O 8
		BOSS NMLKJI  8
		Goto See
	}
}

// Baron slime ball

ACTOR IDMBaronBall : BaronBall replaces BaronBall
{
	Game Doom
	SpawnID 154
	Radius 6
	Height 16
	Speed 18
	FastSpeed 24
	Damage 8
	Projectile
	Species "Bruiser"
	+DONTHURTSPECIES
	+RANDOMIZE
	RenderStyle Add
	Alpha 1
	SeeSound "baron/attack"
	DeathSound "baron/shotx"
	Decal "BaronScorch"
	DamageType "GreenFire"
	States
	{
	Spawn:
		BAL7 AB 4 BRIGHT
		Loop
	Death:
		BAL7 CDE 6 BRIGHT
		Stop
	}
}

ACTOR IDMBigBaronBall : IDMBaronBall
{
   Speed 22
   Damage 12
   States
   {
   Spawn:
      HECF AB 4 BRIGHT
      Loop
   Death:
      HECF C 8 BRIGHT
      HECF D 6 BRIGHT
      HECF E 4 BRIGHT
      Stop
   }
}