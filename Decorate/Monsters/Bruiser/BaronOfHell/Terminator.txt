/// Strogg Terminator

ACTOR IDMTerminator
{
	Health 2000
	Radius 24
	Height 64
	Mass 2000
	Speed 8
	PainChance 10
	Monster
	Scale 1.2
	Species "Bruiser"
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+FLOORCLIP
	+BOSS
	+BOSSDEATH
	SeeSound "monster/termsit"
	PainSound "monster/termpin"
	DeathSound "monster/termdth"
	ActiveSound "monster/termact"
	Obituary "%o was terminated by a strogg terminator."
	DamageFactor "CybruiserBullet", 0.0
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "GreenFire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	DamageType "CybruiserBullet"
	Tag "Strogg Terminator"
	DropItem "IDMHealthBonusPickup", 128
	DropItem "IDMArmorBonusPickup", 128
	DropItem "IDMHealthBonusPickup", 128
	DropItem "IDMArmorBonusPickup", 128
	DropItem "Clip", 64, 20
	DropItem "Clip", 64, 20
	DropItem "ClipBox", 32, 50
	DropItem "Shell", 64, 4
	DropItem "Shell", 64, 20
	DropItem "ShellBox", 32, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketBox", 32, 5
	DropItem "Cell", 64, 20
	DropItem "Cell", 64, 20
	DropItem "Cellpack", 16, 100
	DropItem "IDMMana", 16
	DropItem "IDMMana", 16
	DropItem "IDMMaceAmmo", 16
	DropItem "IDMMaceAmmo", 16
	DropItem "IDMCatAmmo", 16
	DropItem "IDMCatAmmo", 16
	DropItem "IDMGas", 16
	DropItem "IDMGas", 16
	States
	{
	Spawn:
	  TERM AB 10 A_Look
	  Loop
	See:
	  TERM A 1 A_Playsound("monster/bruwlk")
	  TERM A 2 A_Chase
	  TERM A 3 A_Chase
	  TERM BB 3 A_Chase
	  TERM C 1 A_Playsound("monster/bruwlk")
	  TERM C 2 A_Chase
	  TERM C 3 A_Chase 
	  TERM DD 3 A_Chase
	  Loop
	Melee:
	Missile:
	  TNT1 A 0 A_Jump(128,"ChaingunAtk","PlasmaAtk")
	  TERM EE 7 A_FaceTarget
	  TNT1 A 0 A_FaceTarget
	  TNT1 A 0 A_JumpIfCloser(640,"CloseMissile")
	  TNT1 A 0 A_Jump(256,"RocketAtk","MiniAtk","SeekerAtk")
	  Goto RocketAtk
	CloseMissile:
	  TNT1 A 0 A_Jump(256,"RocketAtk","GrenadeAtk","MiniAtk","SeekerAtk")
	RocketAtk:
	  TNT1 A 0 A_Playsound("monster/brufir",CHAN_WEAPON)
	  TERM F 8 Bright A_CustomMissile("MonsterRocket",38,15)
	  TERM E 8
	  Goto See
	GrenadeAtk:
	  TERM F 6 Bright A_CustomMissile("MonsterGrenade",38,15)
	  TERM E 6
	  TERM F 6 Bright A_CustomMissile("MonsterGrenade",38,15)
	  TERM E 8
	  Goto See
	MiniAtk:
	  TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
      TERM F 2 Bright A_CustomMissile("MonsterMiniMissile",38,15,0,0)
      TERM E 1
	  TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
      TERM F 2 Bright A_CustomMissile("MonsterMiniMissile",38,15,0,0)
      TERM E 1
	  TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
      TERM F 2 Bright A_CustomMissile("MonsterMiniMissile",38,15,0,0)
      TERM E 1
	  TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
      TERM F 2 Bright A_CustomMissile("MonsterMiniMissile",38,15,0,0)
      TERM E 8
      Goto See
	SeekerAtk:
	  TNT1 A 0 A_Playsound("weapons/heatseekerf",CHAN_WEAPON)
	  TERM F 8 Bright A_CustomMissile("MonsterSeekerMissile",38,15)
	  TERM E 8
	  Goto See
	ChaingunAtk:
	  TERM G 12 A_FaceTarget
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM H 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM I 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	  TNT1 A 0 A_FaceTarget
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM H 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM I 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	  TNT1 A 0 A_FaceTarget
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM H 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM I 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	ChaingunLoop:
	  TNT1 A 0 A_FaceTarget
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM H 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	  TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
	  TERM I 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	  TNT1 A 0 A_Jump(64,"ChaingunEnd")
	  TNT1 A 0 A_MonsterRefire(40,"ChaingunEnd")
	  Goto ChaingunLoop
	ChaingunEnd:
	  TERM G 10
	  Goto See
	PlasmaAtk:
	  //TNT1 A 0 A_Jump(32,"UnmAtk")
	  TERM J 12 A_FaceTarget
	  TNT1 A 0 A_Jump(128,"LaserAtk")
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("InsanityPlasmaBall",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("InsanityPlasmaBall",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("InsanityPlasmaBall",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("InsanityPlasmaBall",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("InsanityPlasmaBall",40,-15,random(-2,2))
	PlasmaLoop:
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("InsanityPlasmaBall",40,-15,random(-2,2))
	  TNT1 A 0 A_Jump(64,"PlasmaEnd")
	  TNT1 A 0 A_MonsterRefire(40,"PlasmaEnd")
	  Goto PlasmaLoop
	PlasmaEnd:
	  TERM J 15
	  Goto See
	LaserAtk:
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("MonsterLaserShot",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("MonsterLaserShot",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("MonsterLaserShot",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("MonsterLaserShot",40,-15,random(-2,2))
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("MonsterLaserShot",40,-15,random(-2,2))
	LaserLoop:
	  TNT1 A 0 A_FaceTarget
	  TERM KK 3 Bright A_CustomMissile("MonsterLaserShot",40,-15,random(-2,2))
	  TNT1 A 0 A_Jump(64,"PlasmaEnd")
	  TNT1 A 0 A_MonsterRefire(40,"PlasmaEnd")
	  Goto LaserLoop
	Pain:
	  TERM L 2
	  TERM L 2 A_Pain
	  Goto See
	Death:
	  TERM M 10 A_Scream
	  TERM NO 10 Bright
	  TERM PQ 6 Bright
	  TNT1 A 0 A_FaceTarget
      TNT1 A 0 A_SpawnItemEx("IDMTerminatorHead", 15, 0, 90, 10, 0, 0, -170, 128)
      TNT1 A 0 A_SpawnItemEx("IDMTerminatorShoulder", 60, 0, 75, 8, 0, 0, -70, 128)
	  TERM R 6 Bright A_SpawnItemEx("IDMTerminatorArm", 60, 0, 0, 3, 0, 0, -90, 128)
	  TERM STUV 6 Bright
	  TERM W 6 A_NoBlocking
	  TERM XY 6
	  TERM Z 6 A_BossDeath
	  TERM "[" -1
	  Stop
	}
}

ACTOR IDMTerminatorArm
{
	Radius 12
	Height 15
	Speed 3
	Mass 1000
	scale 1.2
	+DOOMBOUNCE
      States
	{
	Spawn:
         TARM A 5
         TARM B 5 A_Fall 
         TARM CDE 5
         TARM F -1
         Stop
	}
}

actor IDMTerminatorHead
{
    Radius 6
    Height 8
    Speed 10
    Mass 1000
    +DOOMBOUNCE
    +DROPOFF
    +Missile
    scale 1.2
    
    States
    {
    Spawn:
    	THAD DEFGHABC 2
        loop
    Death:
    	THAD I -1
        loop
    }
}

actor IDMTerminatorShoulder
{
    Radius 8
    Height 8
    Speed 8
    Mass 1000
    +DOOMBOUNCE
    +DROPOFF
    +Missile
    scale 1.2
    
    States
    {
    Spawn:
    	TSHO ABCDEFGH 2
        loop
    Death:
    	TSHO I -1
        loop
    }
}