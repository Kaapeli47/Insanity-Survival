Actor IDMCybruiser2 : IDMCybruiser
{
	Obituary "%o was decimated by an augmented Cybruiser."
	Tag "Cybruiser Mk.II"
	+DONTHARMCLASS
	States
	{
	Spawn:
		CBR2 AB 10 A_Look
		goto Spawn
	See:
		TNT1 A 0 A_Playsound("monster/bruwlk")
		CBR2 AABB 3 A_Chase
		TNT1 A 0 A_Playsound("monster/bruwlk")
		CBR2 CCDD 3 A_Chase
		Loop 
	Pain:
		CBR2 J 3 
		CBR2 J 3 A_Pain
		goto See
	Melee:
	Missile:
		CBR2 EE 8 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(320,"CloseMissile")
		TNT1 A 0 A_Jump(256,"RocketAtk","MiniAtk")
	CloseMissile:
		TNT1 A 0 A_Jump(256,"RocketAtk","GrenadeAtk","MiniAtk")
		Goto RocketAtk
	RocketAtk:
		CBR2 E 0 A_Playsound("monster/brufir",CHAN_WEAPON)
		CBR2 F 8 Bright A_CustomMissile("MonsterRocket",38,15,0,0)
		Goto ChaingunAtk
	GrenadeAtk:
		CBR2 F 8 Bright A_CustomMissile("MonsterGrenade",38,15,0,0)
		Goto ChaingunAtk
	MiniAtk:
		TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
		CBR2 F 2 Bright A_CustomMissile("MonsterMiniMissile",38,15,0,0)
		CBR2 E 2
		TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
		CBR2 F 2 Bright A_CustomMissile("MonsterMiniMissile",38,15,0,0)
		CBR2 E 2
		TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
		CBR2 F 2 Bright A_CustomMissile("MonsterMiniMissile",38,15,0,0)
	ChaingunAtk:
		CBR2 GS 10 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
		CBR2 H 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
		TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
		CBR2 I 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
		CBR2 H 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
		TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
		CBR2 I 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
	ChaingunLoop:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
		CBR2 H 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
		TNT1 A 0 A_PlaySound("monster/brumgn", CHAN_WEAPON,1,0,0.75)
		CBR2 I 3 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "CybruiserBulletPuff", 0, CBAF_NORANDOM)
		TNT1 A 0 A_Jump(128,"ChaingunEnd")
		TNT1 A 0 A_MonsterRefire(40,"ChaingunEnd")
		Goto ChaingunLoop
	ChaingunEnd:
		CBR2 S 10 Bright A_FaceTarget
		Goto See
	Death:
		CBR2 K 8 
		CBR2 L 8 A_Scream
		CBR2 M 8 A_Fall
		CBR2 NOPQ 8 
		CBR2 R -1
		stop
	Raise:
		CBR2 RQPONMLK 5 
		goto See
	}
}

ACTOR CybruiserBulletPuff : IDMBulletPuff
{
	DamageType "CybruiserBullet"
}