/// Agathodemon

actor IDMAgathodemon : IDMCacoDemon
{
  MONSTER
  Species "Cacodemon"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +FLOAT
  +NOGRAVITY
  +MISSILEMORE
  Radius 31
  Height 56
  Health 600
  Mass 2000
  Speed 10
  PainChance 160
  SeeSound "caco/sight"
  PainSound "caco/pain"
  DeathSound "caco/death"
  ActiveSound "caco/active"
  AttackSound "caco/melee"
  MeleeSound "imp/claw"
  MeleeDamage 9
  MissileType IDMAgathoDemonFireBall
  MissileHeight 24
  Obituary "%o was blasted by an agathodemon."
  HitObituary "%o got caught in an agathodemon's jaws."
  Bloodcolor "Red"
  Tag "Agathodemon"
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  DropItem "Clip", 64, 20
  DropItem "Clip", 24, 20
  DropItem "ClipBox", 8, 50
  DropItem "Shell", 64, 4
  DropItem "Shell", 24, 20
  DropItem "ShellBox", 8, 10
  DropItem "RocketAmmo", 64, 1
  DropItem "RocketAmmo", 24, 1
  DropItem "RocketBox", 8, 5
  DropItem "Cell", 32, 20
  DropItem "Cell", 12, 20
  DropItem "Cellpack", 4, 100
  DropItem "IDMMana", 4
  DropItem "IDMMana", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMGas", 4
  DropItem "IDMGas", 4
  States
  {
  Spawn:
    AGAH A 1 A_Look
    Loop
  See:
    AGAH A 3 A_Chase
    Loop
  Melee:
    AGAH BC 5 A_FaceTarget
    AGAH D 6 A_MeleeAttack
    Goto See
  Missile:
    AGAH D 0 A_Jump(55,"Stealth")
    AGAH D 0 A_Jump(128,"Fireball")
    Goto Tracer
  Stealth:
    AGAH D 0 A_JumpIfInventory("IDMAgathodemonStealthCheck",1,16)
    AGAH BC 5 Bright A_FaceTarget
    AGAH D 2 Bright A_PlaySound("agatho/stealth")
    AGAH D 0 A_UnSetShootable
    AGAH DDDDDDDDD 2 Bright A_FadeOut(0.075)
    AGAH D 0 A_SetShootable
    AGAH D 0 A_GiveInventory("IDMAgathodemonStealthCheck",1)
    Goto See
    AGAH BC 5 Bright A_FaceTarget
    AGAH D 2 Bright A_PlaySound("agatho/stealth")
    AGAH D 0 A_UnSetShootable
    AGAH DDDDDDDDD 2 Bright A_FadeIn(0.075)
    AGAH D 0 A_SetShootable
    AGAH A 0 A_TakeInventory("IDMAgathodemonStealthCheck",1)
    Goto See
  Fireball:
    AGAH BC 4 A_FaceTarget
    AGAH D 5 Bright A_MissileAttack
    AGAH D 0 A_Jump(70,1)
    Goto See
    AGAH D 0
    Goto Tracer
  Tracer:
    AGAH BC 4 A_FaceTarget
    AGAH D 0 A_CustomMissile("IDMAgathoDemonTracer",22,0,-30,0)
    AGAH D 5 A_CustomMissile("IDMAgathoDemonTracer",22,0,30,0)
    AGAH D 0 A_Jump(90,1)
    Goto See
    AGAH D 0
    Goto Fireball
  Pain:
    AGAH F 3
    AGAH F 1 A_Pain
    AGAH E 4
    Goto See
  Death:
    AGAH G 8
    AGAH H 8 A_Scream
    AGAH I 8
    AGAH J 8 A_SetTranslucent(1.0,0)
    AGAH K 8 A_Fall
    AGAH L -1 A_SetFloorClip
    Stop
  Raise:
    AGAH L 8 A_UnSetFloorClip
    AGAH KJIHG 8
    Goto See
  }
}

actor IDMAgathodemonStealthCheck : Inventory
{
  Inventory.Amount 1
  Inventory.MaxAmount 1
}

actor IDMAgathoDemonFireBall : DoomImpBall //trail projectile
{
  Speed 20
  Damage 7
  Decal DoomImpScorch
  DamageType "RedPlasma"
  States
  {
  Spawn:
    TNT1 A 2 Bright A_SpawnItemEx("IDMAgathoDemonFX", 0, 0, 0, 0, 0, 0, 0, 128)
    Loop
  Death:
    AGFX GHIJKL 3 Bright
    Stop
  }
}

actor IDMAgathoDemonFX //trail effect
{
  Scale 0.75
  RenderStyle Add
  +CLIENTSIDEONLY
  +NOGRAVITY
  States
  {
  Spawn:
    AGFX ABCDEF 2 Bright
    Stop
  }
}

actor IDMAgathoDemonTracer : DoomImpBall //homing fireball
{
  +SEEKERMISSILE
  Speed 15
  Damage 4
  Decal BaronScorch
  DamageType "RedPlasma"
  States
  {
  Spawn:
    AGFB ABCABCABCABCABCABCABCABCABCABCABCABC 4 Bright A_Tracer2
  Death:
    AGFB DEFG 4 Bright
    Stop
  }
}