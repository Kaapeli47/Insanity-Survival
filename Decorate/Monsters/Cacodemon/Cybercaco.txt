/// Cyber Cacodemon

ACTOR IDMCybercaco : IDMCacodemon
{
	Health 400
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "GreenFire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	Obituary "%o was evaporated by a cyber cacodemon."
	HitObituary "%o was chomped between a cyber cacodemon's metal jaws."
	Tag "Cyber Cacodemon"
	SeeSound "cybercaco/sight"
	PainSound "cybercaco/pain"
	DeathSound "cybercaco/death"
	ActiveSound "cbaron/active"
	States
	{
	Spawn:
	  CABT A 10 A_Look
	  Loop
	See:
	  CABT A 3 A_Chase
	  Loop
	Missile:
	  TNT1 A 0 A_Jump(32,"UnmAtk")
	  TNT1 A 0 A_Jump(80,"LaserAtk","PlasmaAtk")
	  CABT BC 5 A_FaceTarget
	  CABT D 5 Bright A_BasicAttack (12, "None", "IDMAbaddonBall", 32)
	  Goto See
	UnmAtk:
	  TNT1 A 0 A_Playsound("weapons/unmakerchrg",CHAN_VOICE)
	  CABT B 15 A_FaceTarget
	  CABT M 15 Bright A_FaceTarget
	  TNT1 A 0 A_Playsound("weapons/unmakerfire",CHAN_BODY)
	  CABT N 20 Bright A_CustomMissile("MonsterUnmakerRay",35,0,angle,CMF_ABSOLUTEANGLE)
	  CABT B 15
	  Goto See
	LaserAtk:
	  CABT BM 5 Bright A_FaceTarget
	  CABT N 2 Bright A_CustomMissile("MonsterLaserShot",35,0,frandom(-5,5))
	  CABT M 2 Bright A_CustomMissile("MonsterLaserShot",35,0,frandom(-5,5))
	  CABT N 2 Bright A_CustomMissile("MonsterLaserShot",35,0,frandom(-5,3))
	  CABT M 2 Bright A_CustomMissile("MonsterLaserShot",35,0,frandom(-5,5))
	  CABT N 2 Bright A_CustomMissile("MonsterLaserShot",35,0,frandom(-5,5))
	  Goto See
	PlasmaAtk:
	  CABT BM 5 Bright A_FaceTarget
	  CABT N 3 Bright A_CustomMissile("InsanityPlasmaBall",35,0,frandom(-5,5))
	  CABT M 3 Bright A_CustomMissile("InsanityPlasmaBall",35,0,frandom(-5,5))
	  CABT N 3 Bright A_CustomMissile("InsanityPlasmaBall",35,0,frandom(-5,5))
	  CABT M 3 Bright A_CustomMissile("InsanityPlasmaBall",35,0,frandom(-5,5))
	  CABT B 10 Bright
	  Goto See
	Pain:
	  CABT F 3
	  CABT F 3 A_Pain
	  CABT E 6
	  Goto See
	Death:
	  CABT G 8 A_NoBlocking
	  CABT H 8 A_Scream
	  CABT IJK 8
	  CABT L -1 A_SetFloorClip
	  Stop
	Raise:
	  CABT L 8 A_UnSetFloorClip
	  CABT KJIHG 8
	  Goto See
	}
}