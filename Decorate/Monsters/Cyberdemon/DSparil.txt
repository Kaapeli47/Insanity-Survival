/// D'Sparil

ACTOR IDMSorcerer1 : Sorcerer1
{
  Health 3000
  Radius 28
  Height 100
  Mass 10000
  Speed 16
  PainChance 10
  Monster
  Species "Cyberdemon"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +BOSS
  +DONTMORPH
  -NOTARGET
  +NOICEDEATH
  +FLOORCLIP
  +DONTGIB
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  -NORADIUSDMG
  SeeSound "dsparilserpent/sight"
  AttackSound "dsparilserpent/attack"
  PainSound "dsparilserpent/pain"
  DeathSound "dsparilserpent/death"
  ActiveSound "dsparilserpent/active"
  DamageFactor "CyberDemonBullet", 0.0
  DamageFactor "Rocket", 0.75
  DamageFactor "Grenade", 0.75
  DamageFactor "Fire", 0.75
  DamageFactor "GreenFire", 0.75
  DamageFactor "PlayerRocket", 0.75
  DamageFactor "PlayerGrenade", 0.75
  Obituary "$OB_DSPARIL1"
  HitObituary "$OB_DSPARIL1HIT"
  Tag "D'Sparil on a Serpent"
  States
  {
  Spawn:
    SRCR AB 10 A_Look
    Loop
  See:
    SRCR ABCD 4 A_Sor1Chase
    Loop
  Pain:
    SRCR Q 3 A_Sor1Pain
    Goto See
  Missile:
	TNT1 A 0 A_JumpIfHealthLower(2000,"FuriousMissile")
	TNT1 A 0 A_JumpIfHealthLower(4000,"AngryMissile")
    SRCR Q 6 A_FaceTarget
    SRCR R 6 A_FaceTarget
	TNT1 A 0 A_Jump(128,"FlameAtk","RapidAtk")
    SRCR S 10 Bright A_CustomMissile("IDMSorcererFX1",62)
    Goto See
  FlameAtk:
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	Goto See
  RapidAtk:
    SRCR S 6 Bright A_CustomMissile("IDMSorcererFX1",62)
    SRCR Q 6 A_FaceTarget
    SRCR R 6 A_FaceTarget
    SRCR S 6 Bright A_CustomMissile("IDMSorcererFX1",62)
  RapidLoop:
    SRCR Q 6 A_FaceTarget
    SRCR R 6 A_FaceTarget
    SRCR S 6 Bright A_CustomMissile("IDMSorcererFX1",62)
	TNT1 A 0 A_Jump(192,"See")
	TNT1 A 0 A_MonsterRefire(20,"See")
    Goto RapidLoop
  AngryMissile:
    SRCR Q 6 A_FaceTarget
    SRCR R 6 A_FaceTarget
	TNT1 A 0 A_Jump(128,"FlameAtk2","RapidAtk2")
	TNT1 A 0 A_CustomMissile("IDMSorcererFX1",62,0,-5)
	TNT1 A 0 A_CustomMissile("IDMSorcererFX1",62,0,5)
    SRCR S 8 Bright A_CustomMissile("IDMSorcererFX1",62)
	Goto See
  FlameAtk2:
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	Goto See
  RapidAtk2:
    SRCR Q 5 A_FaceTarget
    SRCR R 5 A_FaceTarget
    SRCR S 5 Bright A_CustomMissile("IDMSorcererFX1",62)
    SRCR Q 5 A_FaceTarget
    SRCR R 5 A_FaceTarget
    SRCR S 5 Bright A_CustomMissile("IDMSorcererFX1",62)
  RapidLoop2:
    SRCR Q 5 A_FaceTarget
    SRCR R 5 A_FaceTarget
    SRCR S 6 Bright A_CustomMissile("IDMSorcererFX1",62)
	TNT1 A 0 A_Jump(128,"See")
	TNT1 A 0 A_MonsterRefire(20,"See")
    Goto RapidLoop2
  FuriousMissile:
    SRCR Q 5 A_FaceTarget
    SRCR R 5 A_FaceTarget
	TNT1 A 0 A_Jump(128,"FlameAtk3","RapidAtk3")
	TNT1 A 0 A_CustomMissile("IDMSorcererFX1",62,0,-10)
	TNT1 A 0 A_CustomMissile("IDMSorcererFX1",62,0,-5)
	TNT1 A 0 A_CustomMissile("IDMSorcererFX1",62,0,5)
	TNT1 A 0 A_CustomMissile("IDMSorcererFX1",62,0,10)
    SRCR S 7 Bright A_CustomMissile("IDMSorcererFX1",62)
	Goto See
  FlameAtk3:
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	TNT1 A 0 A_FaceTarget
    SRCR SS 2 Bright A_CustomMissile("IDMSorcererFX2",62)
	Goto See
  RapidAtk3:
    SRCR Q 4 A_FaceTarget
    SRCR R 4 A_FaceTarget
    SRCR S 4 Bright A_CustomMissile("IDMSorcererFX1",62)
    SRCR Q 4 A_FaceTarget
    SRCR R 4 A_FaceTarget
    SRCR S 4 Bright A_CustomMissile("IDMSorcererFX1",62)
  RapidLoop3:
    SRCR Q 4 A_FaceTarget
    SRCR R 4 A_FaceTarget
    SRCR S 4 Bright A_CustomMissile("IDMSorcererFX1",62)
	TNT1 A 0 A_Jump(64,"See")
	TNT1 A 0 A_MonsterRefire(20,"See")
    Goto RapidLoop3
  Death:
    SRCR E 7
    SRCR F 7 A_Scream
    SRCR G 7
    SRCR HIJK 6
    SRCR L 25
	TNT1 A 0 A_PlaySound("dsparil/zap", CHAN_BODY, 1, FALSE, ATTN_NONE)
    SRCR MN 5 Bright
    SRCR O 4 Bright
    SRCR L 20
	TNT1 A 0 A_PlaySound("dsparil/zap", CHAN_BODY, 1, FALSE, ATTN_NONE)
    SRCR MN 5 Bright
    SRCR O 4 Bright
    SRCR L 12 Bright
    SRCR P -1 A_SorcererRise
	Stop
  }
}

ACTOR IDMSorcererFX1 : SorcererFX1 replaces SorcererFX1
{
  Radius 10
  Height 10
  Speed 30
  FastSpeed 30
  Damage 20
  Scale 1.3
  DamageType Fire
  Projectile
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  RenderStyle Add
  SeeSound "DemonMissileFire"
  DeathSound "DemonMissileExplode"
  States
  {
  Spawn:
    FX14 ABC 6 Bright
    Loop
  Death:
    FX14 DEFGH 5 Bright
    Stop
  }
}

ACTOR IDMSorcererFX2 : IDMSorcererFX1
{
	Speed 25
	FastSpeed 30
	Damage 6
}

ACTOR IDMSorcerer2 : Sorcerer2 replaces Sorcerer2
{
  Health 3000
  Radius 16
  Height 70
  Mass 10000
  Speed 8
  PainChance 5
  Monster
  Species "Cyberdemon"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  -DROPOFF
  +BOSS
  +DONTMORPH
  -FULLVOLACTIVE
  +NOTARGET
  +NOICEDEATH
  +FLOORCLIP
  +BOSSDEATH
  +QUICKTORETALIATE
  +MISSILEMORE
  +MISSILEEVENMORE
  -NORADIUSDMG
  SeeSound "dsparil/sight"
  AttackSound "dsparil/attack2"
  PainSound "dsparil/pain"
  ActiveSound "dsparil/active"
  DamageFactor "CyberDemonBullet", 0.0
  DamageFactor "Rocket", 0.75
  DamageFactor "Grenade", 0.75
  DamageFactor "Fire", 0.75
  DamageFactor "PlayerRocket", 0.75
  DamageFactor "PlayerGrenade", 0.75
  Obituary "$OB_DSPARIL2"
  HitObituary "$OB_DSPARIL2HIT"
  Tag "D'Sparil"
  DropItem "IDMHealthBonusPickup", 256
  DropItem "IDMArmorBonusPickup", 256
  DropItem "IDMHealthBonusPickup", 128
  DropItem "IDMArmorBonusPickup", 128
  DropItem "Clip", 128, 20
  DropItem "Clip", 128, 20
  DropItem "ClipBox", 64, 50
  DropItem "Shell", 128, 4
  DropItem "Shell", 128, 4
  DropItem "ShellBox", 64, 10
  DropItem "RocketAmmo", 128, 1
  DropItem "RocketAmmo", 128, 1
  DropItem "RocketBox", 64, 5
  DropItem "Cell", 128, 20
  DropItem "Cell", 128, 20
  DropItem "Cellpack", 64, 100
  DropItem "IDMMana", 64
  DropItem "IDMMana", 64
  DropItem "IDMMaceAmmo", 64
  DropItem "IDMMaceAmmo", 64
  DropItem "IDMCatAmmo", 64
  DropItem "IDMCatAmmo", 64
  DropItem "IDMGas", 64
  DropItem "IDMGas", 64
  DropItem "QuadShotgun", 1
  DropItem "HandCannon", 1
  DropItem "IDMMinigun", 1
  DropItem "Stinger", 1
  DropItem "HellstormCannon", 1
  DropItem "Devastator", 1
  DropItem "BarrelGun", 1
  DropItem "FreezeThrower", 1
  DropItem "WidePlasmaGun", 1
  States
  {
  Spawn:
    SOR2 MN 10 A_Look
    Loop
  See:
    SOR2 MMNNOOPP 2 A_Chase
    Loop
  Rise:
    SOR2 AB 4 Bright
    SOR2 C 4 A_PlaySound("dsparil/rise", CHAN_BODY, 1, FALSE, ATTN_NONE)
    SOR2 DEF 4
    SOR2 G 12 A_PlaySound("dsparil/sight", CHAN_BODY, 1, FALSE, ATTN_NONE)
    Goto See
  Pain:
    SOR2 Q 3
    SOR2 Q 3 A_Pain
	TNT1 A 0 A_Jump(64,"TeleportAtk")
	SOR2 Q 3
    Goto See
  Missile:
	TNT1 A 0 A_Jump(64,"TeleportAtk")
	TNT1 A 0 A_Jump(80,"RailMissile")
	TNT1 A 0 A_FaceTarget
    SOR2 R 6 Bright A_Srcr2Decide
    SOR2 S 6 Bright A_FaceTarget
    SOR2 T 10 Bright A_Srcr2Attack
	TNT1 A 0 A_Jump(64,"Missile")
    Goto See
  RailMissile:
    SOR2 R 8 Bright A_FaceTarget
    SOR2 S 8 Bright A_FaceTarget
	TNT1 A 0 A_FaceTarget
    SOR2 T 12 Bright A_Playsound("dsparil/attack",CHAN_BODY,1,false,ATTN_NONE)
	SOR2 TTTTTTTT 4 Bright A_CustomRailgun(random(10,30), 0, none, "3399FF",RGF_FULLBRIGHT,1,200,"IDMBulletPuff",0,0,0,15,1.0,0,none,8)
	TNT1 A 0 A_Jump(64,"Missile")
	Goto See
  TeleportAtk:
	SOR2 G 5 Bright
	SOR2 H 5 Bright A_Playsound("dsparil/teleport",CHAN_BODY,1,false,ATTN_NONE)
	SOR2 I 4 Bright A_ChangeFlag(NOBLOCKMAP,true)
	SOR2 JKL 4 Bright
	TNT1 A 0 A_Jump(128,"TeleportEnd")
  TeleportLoop:
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Wander
	TNT1 A 0 A_Jump(64,"TeleportLoop")
  TeleportEnd:
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
	TNT1 A 6
	SOR2 L 4 Bright A_Playsound("dsparil/teleport",CHAN_BODY,1,false,ATTN_NONE)
	SOR2 KJI 4 Bright
	SOR2 H 4 Bright
	SOR2 G 4 Bright A_ChangeFlag(NOBLOCKMAP,false)
	Goto See
  Death:
    SDTH A 8 Bright
    SDTH B 8 Bright
    SDTH C 8 Bright A_PlaySound("dsparil/scream", CHAN_BODY, 1, FALSE, ATTN_NONE)
	SDTH DEF 7 Bright
	SDTH DEF 6 Bright
	SDTH DEF 5 Bright
	SDTH DEFDEFDEF 4 Bright
	SDTH DEFDEFDE 3 Bright
	SDTH FDEFDEFDEFDEFDEFDE 2 Bright
    SDTH G 6 Bright A_PlaySound("dsparil/explode", CHAN_BODY, 1, FALSE, ATTN_NONE)
    SDTH H 6 Bright
    SDTH I 50
    SDTH J 6 A_NoBlocking
    SDTH K 6 A_PlaySound("dsparil/bones", CHAN_BODY, 1, FALSE, ATTN_NONE)
    SDTH LMN 6
    SDTH O -1 A_BossDeath
    Stop
  }
}

ACTOR IDMSorcerer2FX1 : Sorcerer2FX1 replaces Sorcerer2FX1
{
  Radius 10
  Height 6
  Speed 25
  FastSpeed 30
  Damage 25
  Projectile
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  RenderStyle Add
  States
  {
  Spawn:
    FX16 ABC 3 Bright A_BlueSpark
    Loop
  Death:
    FX16 G 5 Bright A_Explode(random[S2FX1](160,111))
    FX16 HIJKL 5 Bright
    Stop
  }
}

ACTOR IDMSorcerer2FX2 : Sorcerer2FX2 replaces Sorcerer2FX2
{
  Radius 10
  Height 6
  Speed 6
  Damage 10
  Projectile
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  RenderStyle Add
  States
  {
  Spawn:
    FX11 A 35 Bright
    FX11 A 5 Bright A_GenWizard
    FX11 B 5 Bright
    Goto Spawn+1
  Death:
    FX11 CDEFG 5 Bright
    Stop
  }
}