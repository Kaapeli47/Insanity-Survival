/// Blood Demon

ACTOR IDMBloodDemon : IDMDemon
{
	SpawnID 156
	Health 300
	GibHealth 100
	Speed 14
	Mass 1000
	Tag "Blood Demon"
	Obituary "%o was chomped on by a blood demon."
	DropItem "IDMHealthBonusPickup", 32
	DropItem "IDMArmorBonusPickup", 32
	DropItem "Clip", 64, 20
	DropItem "ClipBox", 8, 50
	DropItem "Shell", 64, 4
	DropItem "ShellBox", 8, 10
	DropItem "RocketAmmo", 64, 1
	DropItem "RocketBox", 8, 5
	DropItem "Cell", 32, 20
	DropItem "Cellpack", 4, 100
	DropItem "IDMMana", 4
	DropItem "IDMMaceAmmo", 4
	DropItem "IDMCatAmmo", 4
	DropItem "IDMGas", 4
	States
	{
	Spawn:
		SRG2 AB 10 A_Look
		Loop
	See:
		SRG2 AABBCCDD 2 Fast A_Chase
		Loop
	Pain:
		SRG2 H 2
		SRG2 H 2 A_Pain
		Goto See
	Melee:
		SRG2 EF 6 Fast A_FaceTarget
		SRG2 G 6 Fast A_CustomMeleeAttack(random(1,10)*8,none,none,"Punch",true)
		Goto See
	Death:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
		SRG2 I 8
		SRG2 J 8 A_Scream
		SRG2 K 4
		SRG2 L 4 A_NoBlocking
		SRG2 M 4
		SRG2 N -1
		Stop
	XDeath:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
		SRG2 O 5
		SRG2 P 5 A_XScream
		SRG2 Q 5
		SRG2 R 4 A_NoBlocking
		SRG2 STUV 4
		SRG2 W -1
		Stop
	XDeath.Resurrector:
		SRG2 O 5
		SRG2 P 5 A_XScream
		SRG2 Q 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		SRG2 R 4 A_NoBlocking
		SRG2 STUV 4
		SRG2 W -1
		Stop
	Raise:
		SRG2 NMLKJI 5
		Goto See
	}
}