ACTOR IDMHarkubus : IDMFatso
{
	Speed 7
	Health 750
	Painchance 20
	Tag "Harkubus"
	Obituary "%o was bombed by a Harkubus."
	SeeSound "harkubus/sight"
	ActiveSound "harkubus/active"
	PainSound "harkubus/pain"
	DeathSound "harkubus/death"
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "GreenFire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	-MISSILEMORE
	States
	{
	Spawn:
	  HARK AB 15 A_Look
	  Loop
	See:
	  TNT1 A 0 A_Playsound("incubus/walk",CHAN_6)
	  HARK AA 3 A_Chase
	  TNT1 A 0 A_Playsound("cbaron/metal")
	  HARK BBCC 3 A_Chase
	  TNT1 A 0 A_Playsound("incubus/walk",CHAN_6)
	  HARK DD 3 A_Chase
	  TNT1 A 0 A_Playsound("cbaron/metal")
	  HARK EEFF 3 A_Chase
	  Loop
	Missile:
	  TNT1 A 0 A_FaceTarget
	  HARK G 20 A_Playsound("harkubus/attack")
	  TNT1 A 0 A_FaceTarget
	  TNT1 A 0 A_CustomMissile("IDMImpRocket", 34, 28, 0)
	  HARK H 10 BRIGHT A_CustomMissile("IDMImpRocket", 34, -28, 11.25)
	  HARK IG 5 A_FaceTarget
	  TNT1 A 0 A_FaceTarget
	  TNT1 A 0 A_CustomMissile("IDMImpRocket", 34, 28, -11.25)
	  HARK H 10 BRIGHT A_CustomMissile("IDMImpRocket", 34, -28, 0)
	  HARK IG 5 A_FaceTarget
	  TNT1 A 0 A_FaceTarget
	  TNT1 A 0 A_CustomMissile("IDMImpRocket", 34, 28, -5.625)
	  HARK H 10 BRIGHT A_CustomMissile("IDMImpRocket", 34, -28, 5.625)
	  HARK IG 5 A_FaceTarget
	  Goto See
	Pain:
	  HARK J 3
	  HARK J 3 A_Pain
	  Goto See
	Death:
	  HARK K 6
	  HARK L 6 A_Scream
	  HARK M 6 A_NoBlocking
	  HARK NOPQRS 6
	  HARK T -1 A_BossDeath
	  Stop
	Ice:
	  "####" "#" 5 A_GenericFreezeDeath
	  TNT1 A 0 A_BossDeath
	  "----" A 1 A_FreezeDeathChunks
	  Wait
    Raise:
	  HARK R 5
	  HARK QPONMLK 5
	  Goto See
	}
}