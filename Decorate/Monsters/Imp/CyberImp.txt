/// CyberImp

ACTOR IDMCyberImp : IDMDoomImp
{
  Health 90
  GibHealth 90
  Mass 200
  PainChance 150
  SeeSound "cyberimp/sight"
  PainSound "cyberimp/pain"
  DeathSound "cyberimp/death"
  ActiveSound "cyberimp/active"
  MeleeDamage 6
  DamageFactor "Rocket", 0.75
  DamageFactor "Grenade", 0.75
  DamageFactor "Fire", 0.75
  DamageFactor "GreenFire", 0.75
  DamageFactor "PlayerRocket", 0.75
  DamageFactor "PlayerGrenade", 0.75
  Tag "Cyber Imp"
  HitObituary "%o was torn apart by a cyber imp."
  Obituary "%o was fried by a cyber imp."
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  DropItem "Clip", 32, 20
  DropItem "Shell", 32, 4
  DropItem "RocketAmmo", 32, 1
  DropItem "Cell", 32, 20
  DropItem "IDMMana", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMGas", 4
  States
  {
  Spawn:
    CIMP AB 10 A_Look
    Loop
  See:
    CIMP AABBCCDD 3 A_Chase
    Loop
  Melee:
    CIMP EF 8 A_FaceTarget
    CIMP F 6 A_MeleeAttack
    Goto See
  Missile:
    CIMP EF 8 A_FaceTarget
  MissileBurst:
	TNT1 A 0 A_FaceTarget
    CIMP G 2 A_Custommissile("MonsterPlasmaBall",36,4)
	TNT1 A 0 A_Jump(128,"MissileBurst")
    Goto See
  Pain:
    CIMP H 2
    CIMP H 2 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    CIMP I 8
    CIMP J 8 A_Scream
    CIMP K 6
    CIMP L 6 A_NoBlocking
    CIMP M -1
    Stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    CIMP N 5 A_CustomMissile("IDMCyberArm",32,-10,270)
    CIMP O 5 A_XScream
    CIMP P 5
    CIMP Q 5 A_NoBlocking
    CIMP RST 5
    CIMP U -1
    Stop
  XDeath.Resurrector:
	CIMP N 5 A_CustomMissile("IDMCyberArm",32,-10,270)
	CIMP O 5 A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	CIMP P 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	CIMP Q 5 A_NoBlocking
	CIMP RST 5
	CIMP U -1
	Stop
  Raise:
    CIMP MLKJI 8
    Goto See
  }
}

ACTOR IDMCyberArm
{
	Radius 8
	Height 8
	Speed 4
	+DOOMBOUNCE
	+DROPOFF
	+Missile
      States
	{
	Spawn:
         CARM ABCDE 3
         Loop
	Death:
	 CARM F -1
	 Stop
	}
}