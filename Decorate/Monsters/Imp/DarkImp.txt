/// DarkImp

actor IDMDarkImp : IDMDoomImp
{
  Tag "Dark Imp"
  obituary "%o was burned by a dark imp."
  hitobituary "%o was slashed by a dark imp."
  health 90
  GibHealth 90
  states
  {
  Spawn:
    DIMP AB 10 A_Look
    loop
  See:
    DIMP AABBCCDD 3 A_Chase
    loop
  Melee:
  Missile:
    DIMP EF 6 A_FaceTarget
    DIMP G 5 A_CustomComboAttack("IDMDarkImpBall", 32, 6*random(1,8), "imp/melee")
    goto See
  Pain:
    DIMP H 2
    DIMP H 2 A_Pain
    goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    DIMP I 8
    DIMP J 8 A_Scream
    DIMP K 6
    DIMP L 6 A_NoBlocking
    DIMP M -1
    stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    DIMP N 5
    DIMP O 5 A_XScream
    DIMP P 5
    DIMP Q 5 A_NoBlocking
    DIMP RST 5
    DIMP U -1
    stop
  XDeath.Resurrector:
	DIMP N 5
	DIMP O 5 A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	DIMP P 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	DIMP Q 5 A_NoBlocking
	DIMP RST 5
	DIMP U -1
	Stop
  Raise:
    DIMP ML 8
    DIMP KJI 6
    goto See
  }
}

actor IDMDarkImpBall : IDMDoomImpBall
{
  game Doom
  spawnid 218
  Speed 15
  damage 4
  DamageType "Plasma"
  Decal DoomImpScorch
  +SEEKERMISSILE
  states
  {
  Spawn:
    BAL4 AB 4 bright
    loop
  Death:
    BAL4 CDE 6 bright
    stop
  }
}