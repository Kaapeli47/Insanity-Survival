/// Devil

actor IDMDevil : IDMDoomImp
{
  Tag "Devil"
  obituary "%o was destroyed by a devil."
  hitobituary "%o was ripped apart by a devil."
  health 120
  GibHealth 100
  mass 120
  speed 10
  painchance 160
  seesound "monster/dvlsit"
  painsound "monster/dvlpai"
  deathsound "monster/dvldth"
  activesound "monster/dvlact"
  states
  {
  Spawn:
    TRO2 AB 10 A_Look
    loop
  See:
    TRO2 AABBCCDD 3 A_Chase
    loop
  Melee:
  Missile:
    TRO2 EF 6 A_FaceTarget
    TRO2 G 4 A_CustomComboAttack("IDMDoomImpBall", 32, 3*random(1,8), "imp/melee")
    TRO2 B 2
    TRO2 VW 6 A_FaceTarget
    TRO2 X 4 A_CustomComboAttack("IDMDoomImpBall", 32, 3*random(1,8), "imp/melee")
    TRO2 E 0 A_Jump(200,9)
    TRO2 D 2
    TRO2 EF 6 A_FaceTarget
    TRO2 G 4 A_CustomComboAttack("IDMDoomImpBall", 32, 3*random(1,8), "imp/melee")
    TRO2 B 2
    TRO2 VW 6 A_FaceTarget
    TRO2 X 4 A_CustomComboAttack("IDMDoomImpBall", 32, 3*random(1,8), "imp/melee")
    TRO2 B 0
    goto See
  Pain:
    TRO2 H 2
    TRO2 H 2 A_Pain
    goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    TRO2 I 8
    TRO2 J 8 A_Scream
    TRO2 K 6
    TRO2 L 6 A_NoBlocking
    TRO2 M -1
    stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    TRO2 N 5
    TRO2 O 5 A_XScream
    TRO2 P 5
    TRO2 Q 5 A_NoBlocking
    TRO2 RST 5
    TRO2 U -1
    stop
  XDeath.Resurrector:
	TRO2 N 5
	TRO2 O 5 A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	TRO2 P 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	TRO2 Q 5 A_NoBlocking
	TRO2 RST 5
	TRO2 U -1
	Stop
  Raise:
    TRO2 ML 8
    TRO2 KJI 6
    goto See
  }
}