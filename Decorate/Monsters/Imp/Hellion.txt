Actor IDMHellion
{
  Tag "Hellion"
  Obituary "%o was burned to a crisp by a hellion."
  Health 140
  GibHealth 100
  Radius 20
  Height 56
  Mass 240
  Speed 10
  PainChance 24
  SeeSound "monster/hlnsit"
  PainSound "monster/hlnpai"
  DeathSound "monster/hlndth"
  ActiveSound "monster/hlnact"
  Monster
  +FloorClip
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  DropItem "Clip", 32, 20
  DropItem "Shell", 32, 4
  DropItem "RocketAmmo", 32, 1
  DropItem "Cell", 16, 20
  DropItem "IDMMana", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMGas", 4
  States
  {
  Spawn:
    HELN AB 10 A_Look
    Loop
  See:
    HELN AABBCCDDEEFF 2 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_VileStart
    HELN GHIJK 6 Bright A_FaceTarget
	TNT1 A 0 A_FaceTarget
    HELN KK 3 Bright A_CustomMissile("IDMHellionBall",32,0,random(-6,6),0,0)
	TNT1 A 0 A_FaceTarget
    HELN KK 3 Bright A_CustomMissile("IDMHellionBall",32,0,random(-6,6),0,0)
	HELN K 4 Bright A_FaceTarget
    HELN L 4
    Goto See
  Pain:
    HELN M 2
    HELN M 2 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    HELN N 6
    HELN O 6 A_Scream
    HELN PQR 6
    HELN S 6 A_NoBlocking
    HELN T -1
    Stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    HELN U 5
    HELN V 5 A_XScream
    HELN W 5
    HELN X 5 A_NoBlocking
    HELN YZ 5
    HEL2 AB 5
    HEL2 C -1
    Stop
  XDeath.Resurrector:
    HELN U 5
    HELN V 5 A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
    HELN W 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
    HELN X 5 A_NoBlocking
    HELN YZ 5
    HEL2 AB 5
    HEL2 C -1
    Stop
  Raise:
    HELN TSRQPON 6
    Goto See
  }
}

Actor IDMHellionBall : CacodemonBall
{
  Damage 8
  Speed 15
  Alpha 0.80
  DamageType Fire
  SeeSound "Monster/hlnatk"
  DeathSound "Monster/hlnexp"
  Decal DoomImpScorch
  states
  {
  Spawn:
    HLBL AB 3 Bright A_SpawnItemEx("IDMHellionPuff",0,0,0,0,0,0,0,128)
    Loop
  Death:
    HLBL JKLMN 3 Bright
    Stop
  }
}

Actor IDMHellionPuff
{
  Radius 3
  Height 3
  RenderStyle Add
  Alpha 0.67
  +NOGRAVITY
  +NOBLOCKMAP
  +DONTSPLASH
  +CLIENTSIDEONLY
  States
  {
  Spawn:
    TNT1 A 3 Bright
    HLBL CDEFGHI 3 Bright
    Stop 
  }
}