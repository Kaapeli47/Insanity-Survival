/// Imp Abomination

Actor IDMImpAbomination : IDMDoomImp
{
	Scale 1.05
	Health 70
	GibHealth 70
	Speed 6
	-MISSILEMORE
	-QUICKTORETALIATE
	Tag "Imp Abomination"
	HitObituary "%o was slashed by an imp abomination."
	Obituary "%o was burned by an imp abomination."
	States
	{
	Spawn:
	  TRO5 AB 10 A_Look
	  Loop
	See:
	  TRO5 AABBCCDD 3 A_Chase
	  Loop
  	Melee:
  	Missile:
	  TRO5 EF 8 A_FaceTarget
	  TRO5 G 6 A_CustomComboAttack("IDMLargeImpBall",32,5*random(1,8),"imp/melee")
	  Goto See
  	Pain:
	  TRO5 H 2
	  TRO5 H 2 A_Pain
	  Goto See
  	Death:
	  TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	  TRO5 I 8
	  TRO5 J 8 A_Scream
	  TRO5 K 6
	  TRO5 L 6 A_NoBlocking
	  TROO M -1
	  Stop
  	XDeath:
	  TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
	  TROO N 5
	  TROO O 5 A_XScream
	  TROO P 5
	  TROO Q 5 A_NoBlocking
	  TROO RST 5
	  TROO U -1
	  Stop
	XDeath.Resurrector:
	  TROO N 5
	  TROO O 5 A_XScream
	  TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	  TROO P 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	  TROO Q 5 A_NoBlocking
	  TROO RST 5
	  TROO U -1
	  Stop
  	Raise:
	  TROO M 8
	  TRO5 L 8
	  TRO5 KJI 6
	  Goto See
	}
}