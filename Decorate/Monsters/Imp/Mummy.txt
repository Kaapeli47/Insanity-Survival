/// Golem

ACTOR IDMMummy : Mummy
{
  Health 100
  GibHealth 100
  Radius 20
  Height 56
  Mass 75
  Speed 12
  PainChance 64
  Monster
  Species "Imp"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +FLOORCLIP
  SeeSound "mummy/sight"
  AttackSound "mummy/attack1"
  PainSound "mummy/pain"
  DeathSound "mummy/death"
  ActiveSound "mummy/active"
  HitObituary "$OB_MUMMY"
  HitObituary "%o was smashed by a nitrogolem."
  Obituary "%o was shrieked to death by a nitrogolem."
  Tag "Nitrogolem"
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  DropItem "Clip", 32, 20
  DropItem "Shell", 32, 4
  DropItem "RocketAmmo", 32, 1
  DropItem "Cell", 16, 20
  DropItem "IDMMana", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMGas", 4
  States
  {
  Spawn:
    MUMM AB 10 A_Look
    Loop
  See:
    MUMM ABCD 4 A_Chase
    Loop
  Melee:
    MUMM E 6 A_FaceTarget
    MUMM F 6 A_CustomMeleeAttack(random[MummyAttack](1,8)*6, "mummy/attack2", "mummy/attack")
    MUMM G 6
    Goto See
  Missile:
    MUMM Y 5 Bright A_FaceTarget
    MUMM X 5 A_FaceTarget
    MUMM Y 5 Bright A_FaceTarget
    MUMM X 5 A_FaceTarget
    MUMM Y 5 Bright A_CustomComboAttack("IDMMummyFX1", 32, random[MummyAttack2](1,8)*6, "mummy/attack2")
    Goto See
  Pain:
    MUMM H 4
    MUMM H 4 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    MUMM I 5
    MUMM J 5 A_Scream
    MUMM K 5 A_SpawnItemEx("MummySoul", 0, 0, 10, 0, 0, 1)
    MUMM L 5
    MUMM M 5 A_NoBlocking
    MUMM NO 5
    MUMM P -1
    Stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    MUMM I 5
    MUMM J 5 A_XScream
    MUMM K 5 A_SpawnItemEx("MummySoul", 0, 0, 10, 0, 0, 1)
    MUMM L 5
    MUMM M 5 A_NoBlocking
    MUMM NO 5
    MUMM P -1
    Stop
  XDeath.Resurrector:
    MUMM I 5
    MUMM J 5 A_XScream
    MUMM K 5 A_SpawnItemEx("MummySoul", 0, 0, 10, 0, 0, 1)
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
    MUMM L 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
    MUMM M 5 A_NoBlocking
    MUMM NO 5
    MUMM P -1
    Stop
  Raise:
    MUMM ONMLKJIH 5
	Goto See
  }
}

ACTOR IDMMummyFX1 : MummyFX1
{
  Radius 8
  Height 14
  Speed 14
  FastSpeed 18
  Damage 4
  RenderStyle Add
  Projectile
  DeathSound "hknight/hit"
  -ACTIVATEPCROSS
  -ACTIVATEIMPACT
  +SEEKERMISSILE
  States
  {
  Spawn:
    FX15 A 5 Bright A_PlaySound("mummy/head")
    FX15 B 5 Bright A_SeekerMissile(21, 42)
    FX15 C 5 Bright
    FX15 B 5 Bright A_SeekerMissile(21, 42)
    Loop
  Death:
    FX15 DEFG 5 Bright
    Stop
  }
}