/// Rocket Imp

ACTOR IDMRocketImp
{
  Health 75
  GibHealth 75
  Radius 20
  Height 56
  Mass 200
  Speed 8
  PainChance 200
  Monster
  Species "Imp"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +DONTHARMCLASS
  +FLOORCLIP
  MissileHeight 30
  SeeSound "cyberimp/sight"
  PainSound "cyberimp/pain"
  DeathSound "cyberimp/death"
  ActiveSound "cyberimp/active"
  DamageFactor "Rocket", 0.75
  DamageFactor "Grenade", 0.75
  DamageFactor "Fire", 0.75
  DamageFactor "GreenFire", 0.75
  DamageFactor "PlayerRocket", 0.75
  DamageFactor "PlayerGrenade", 0.75
  Obituary "%o was blown apart by a rocket imp."
  Tag "Rocket Imp"
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  DropItem "Clip", 32, 20
  DropItem "Shell", 32, 4
  DropItem "RocketAmmo", 64, 1
  DropItem "Cell", 16, 20
  DropItem "IDMMana", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMGas", 4
  States
  {
  Spawn:
    RIMP AB 10 A_Look
    Loop
  See:
    RIMP AABBCCDD 3 A_Chase
    Loop
  Melee:
  Missile:
	RIMP A 3 A_FaceTarget
	TNT1 A 0 A_Jump(128,"MiniAtk")
	TNT1 A 0 A_Jump(128,"RocketAtk2")
  RocketAtk:
    RIMP E 10 A_FaceTarget
    RIMP W 5 BRIGHT A_CustomMissile("IDMImpRocket")
	RIMP E 5 A_FaceTarget
    Goto See
  RocketAtk2:
    RIMP G 10 A_FaceTarget
    RIMP V 5 BRIGHT A_CustomMissile("IDMImpRocket")
	RIMP G 5 A_FaceTarget
    Goto See
  MiniAtk:
    RIMP E 10 A_FaceTarget
    RIMP W 1 BRIGHT 
	TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
    RIMP W 4 BRIGHT A_CustomMissile("MonsterMiniMissile")
	RIMP F 5
	RIMP G 6 A_FaceTarget
    RIMP V 1 BRIGHT
	TNT1 A 0 A_Playsound("weapons/minimf",CHAN_WEAPON)
    RIMP V 4 BRIGHT A_CustomMissile("MonsterMiniMissile")
	RIMP G 5
    Goto See
  Pain:
    RIMP H 2
    RIMP H 2 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    RIMP I 8
    RIMP J 8 A_Scream
    RIMP K 6
    RIMP L 6 A_NoBlocking
    RIMP M -1
    Stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    RIMP N 5 Bright
	RIMP O 0 A_custommissile("IDMRocketarm2",30,-10,270)
	RIMP O 0 A_custommissile("IDMRocketarm",30,10,93)
	TNT1 A 0 A_playsound("weapons/rocklx",CHAN_BODY)
    RIMP O 5 Bright A_XScream
	RIMP P 5 Bright
    RIMP Q 5 Bright A_NoBlocking
    RIMP RST 5 Bright
    RIMP U -1
    Stop
  XDeath.Resurrector:
	RIMP N 5 Bright
	RIMP O 0 A_custommissile("IDMRocketarm2",30,-10,270)
	RIMP O 0 A_custommissile("IDMRocketarm",30,10,93)
	TNT1 A 0 A_playsound("weapons/rocklx",CHAN_BODY)
	RIMP O 5 Bright A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	RIMP P 5 Bright A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	RIMP Q 5 Bright A_NoBlocking
	RIMP RST 5 Bright
	RIMP U -1
	Stop
  Raise:
	TNT1 A 0
	TNT1 A 0 A_RemoveChildren
    RIMP ML 8
    RIMP KJI 6
    Goto See
  }
}

ACTOR IDMImpRocket : MonsterRocket
{
  Radius 6
  Height 1
  Speed 20
  Damage 10
  Projectile
  +RANDOMIZE
  +DEHEXPLOSION
  +ROCKETTRAIL
  Decal RocketImpScorch
  DamageType "Rocket"
  States
  {
  Spawn:
    IMIS A 1 Bright
    Loop
  Death:
    IMIS B 6 Bright A_Explode(100,100)
    IMIS C 5 Bright
    IMIS D 5 Bright
    Stop
  }
}

ACTOR IDMRocketArm
{
	Radius 12
	Height 10
	Speed 4
	+CLIENTSIDEONLY
	+DOOMBOUNCE
	+DROPOFF
	+Missile
    States
	{
	Spawn:
      IARM ABCD 3
      Loop
	Death:
	  IARM E -1
	  Loop
	}
} 

ACTOR IDMRocketArm2
{
	Radius 12
	Height 10
	Speed 4
	+CLIENTSIDEONLY
	+DOOMBOUNCE
	+DROPOFF
	+Missile
    States
	{
	Spawn:
      IAR2 ABCD 3
      Loop
	Death:
	  IAR2 E -1
	  Loop
	}
}