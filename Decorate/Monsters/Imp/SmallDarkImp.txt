/// Small Dark Imp

actor IDMSmallDarkImp : IDMDarkImp
{
	Radius 18
	Height 50
	Scale 0.9
    Health 75
	GibHealth 75
    Speed 12
	+MISSILEMORE
	+MISSILEEVENMORE
	Tag "Small Dark Imp"
    Obituary "%o was burned by a small dark imp."
    HitObituary "%o was slashed by a small dark imp."
    States
    {
    Spawn:
        DRKS AB 10 A_Look
        Loop
    See:
        DRKS AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKS EF 8 A_FaceTarget
        DRKS G 6 A_CustomComboAttack("IDMSmallDarkImpBall", 32, 4*random(1,8), "imp/melee")
        Goto See
    Pain:
        DRKS H 2
        DRKS H 2 A_Pain
        Goto See
    Death:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
        DRKS I 8
        DRKS J 8 A_Scream
        DRKS K 6
        DRKS L 6 A_Fall
        DRKS M -1
        Stop
    XDeath:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
        DIMP N 5
        DIMP O 5 A_XScream
        DIMP P 5
        DIMP Q 5 A_Fall
        DIMP RST 5
        DIMP U -1
        Stop
	XDeath.Resurrector:
		DIMP N 5
		DIMP O 5 A_XScream
		TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
		DIMP P 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		DIMP Q 5 A_NoBlocking
		DIMP RST 5
		DIMP U -1
		Stop
    Raise:
        DRKS ML 8
        DRKS KJI 6
        Goto See
    }
}

actor IDMSmallDarkImpBall : IDMDarkImpBall
{
    Speed 18
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
	Scale 0.75
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    MISSILEHEIGHT 0
    Decal DoomImpScorch
	states
	{
	Spawn:
      BAL4 AB 4 bright
      loop
	Death:
      BAL4 CDE 6 bright
      stop
	}
}

