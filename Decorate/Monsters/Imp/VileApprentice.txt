ACTOR IDMVileApprentice : IDMHellion
{
  Speed 10
  PainChance 24
  MinMissileChance 240
  Tag "Vile Apprentice"
  Obituary "%o received third degree burns from a vile apprentice."
  BloodColor "tan4"
  States
  {
  Spawn:
    VAPP AB 10 A_Look
    Loop
  See:
    VAPP AABBCCDDEEFF 2 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_VileStart
	TNT1 A 0 A_Jump(128,"Missile2")
    VAPP G 8 Bright A_FaceTarget
	VAPP H 8 Bright A_VileTarget("IDMVileApprenticeFire")
	VAPP I 8 Bright A_FaceTarget
	VAPP J 16 Bright A_FaceTarget
	VAPP K 8 Bright A_FaceTarget
    VAPP L 8 Bright A_VileAttack("vile/stop",25,0,0,0.75,"Fire",1)
    VAPP L 8
    Goto See
  Missile2:
    VAPP GHIJK 7 Bright A_FaceTarget
    VAPP L 7 Bright A_CustomMissile("IDMArchMissile",32,0,0,0,0)
    VAPP L 7
    Goto See
  Pain:
    VAPP M 2
    VAPP M 2 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    VAPP N 6
    VAPP O 6 A_Scream
    VAPP PQR 6
    VAPP S 6 A_NoBlocking
    VAPP T -1
    Stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    VAPP U 5
    VAPP V 5 A_XScream
	VAPP W 5
    VAPP X 5 A_NoBlocking
    VAPP YZ 5
    VAP2 AB 5
    VAP2 C -1
    Stop
  XDeath.Resurrector:
    VAPP U 5
    VAPP V 5 A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	VAPP W 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
    VAPP X 5 A_NoBlocking
    VAPP YZ 5
    VAP2 AB 5
    VAP2 C -1
    Stop
  Raise:
    VAPP TSRQPON 6
    Goto See
  }
}


ACTOR IDMVileApprenticeFire : ArchvileFire
{
  +NOBLOCKMAP
  +NOGRAVITY
  RenderStyle Add
  Alpha 1
  Scale 0.65
  States
  {
  Spawn:
    FIRE A 2 Bright A_StartFire
    FIRE BA 1 Bright A_Fire
	FIRE B 2 Bright A_Fire
    FIRE C 1 Bright A_FireCrackle
    FIRE B 1 bright A_Fire
	FIRE C 2 Bright A_Fire
	FIRE BC 1 Bright A_Fire
	FIRE D 2 Bright A_Fire
	FIRE CD 1 Bright A_Fire
	FIRE C 2 Bright A_Fire
	FIRE DE 1 Bright A_Fire
	FIRE D 2 Bright A_Fire
	FIRE ED 1 Bright A_Fire
    FIRE E 2 Bright A_FireCrackle
    FIRE FE 1 Bright A_Fire
	FIRE F 2 Bright A_Fire
	FIRE EF 1 Bright A_Fire
	FIRE G 2 Bright A_Fire
	FIRE HG 1 Bright A_Fire
	FIRE H 2 Bright A_Fire
	FIRE GH 1 Bright A_Fire
    Stop
  }
}