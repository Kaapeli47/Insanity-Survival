ACTOR IDMCacomother : IDMPainElemental
{
	+BOSS
	+NOINFIGHTING
	+NOTARGET
	Health 1500
	Mass 500
	SeeSound "cacomother/sight"
	PainSound "cacomother/pain"
	DeathSound "cacomother/death"
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "GreenFire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	Tag "Cacomother"
	Obituary "%o was matronized by a cacomother."
	Species "Cacodemon"
	States
	{
	Spawn:
	  CAEL A 10 A_Look
	  Loop
	See:
	  CAEL A 3 A_Chase
	  Loop
	Missile:
	  CAEL BC 5 Bright A_FaceTarget
	  TNT1 A 0 A_Jump(96,"ProjAtk","ScatterAtk")
	  CAEL D 5 Bright A_FaceTarget
	  TNT1 A 0 A_PainAttack("IDMBabycacodemon")
	  TNT1 A 0 A_Jump(64,"Missile")
	  Goto See
	Melee:
	  CAEL BC 5 Bright A_FaceTarget
	  CAEL D 5 Bright A_CustomComboAttack("IDMCacolanternBall",32,10*random(1,10),"caco/melee","Punch")
	  Goto See
	ProjAtk:
	  CAEL D 5 Bright A_CustomComboAttack("IDMCacolanternBall",32,10*random(1,10),"caco/melee","Punch")
	  TNT1 A 0 A_Jump(64,"Missile")
	  Goto See
	ScatterAtk:
	  TNT1 AAAAAAAA 0 A_CustomMissile("IDMCacolanternBall",32,0,random(-15,15),CMF_AIMDIRECTION,random(-6,6))
	  CAEL D 5 Bright
	  TNT1 A 0 A_Jump(64,"Missile")
	  Goto See
	Pain:
	  CAEL E 6
	  CAEL E 6 A_Pain
	  Goto See
	Death:
	  CAEL F 8 Bright
	  CAEL G 8 Bright A_Scream
	  CAEL HI 8 Bright
	  CAEL JK 8 Bright A_PainDie("IDMBabyCacodemon")
	  Stop
	}
}