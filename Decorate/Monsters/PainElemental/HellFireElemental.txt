ACTOR IDMHellFireElemental : IDMPainElemental
{
	Tag "Hellfire Elemental"
	Obituary "%o was broiled by a hellfire elemental."
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "GreenFire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	+MISSILEMORE
	States
	{
	Spawn:
		HFEL A 10 A_Look
		Loop
	See:
		HFEL AABBCCBB 3 A_Chase
		Loop
	Missile:
		HFEL DE 5 Bright A_FaceTarget
		TNT1 A 0 A_Jump(128,"FireAtk","RevAtk","FireScatterAtk","RevScatterAtk")
		HFEL F 5 BRIGHT A_FaceTarget
		HFEL F 0 A_PainAttack("IDMLostSoul")
		Goto See
	FireAtk:
		TNT1 A 0 A_FaceTarget
		HFEL FFF 3 Bright A_CustomMissile("IDMFatShot",24,0,random(-12,12))
		Goto See
	RevAtk:
		TNT1 A 0 A_FaceTarget
		HFEL FFFFF 2 Bright A_CustomMissile("IDMRevenantTracer",24,0,random(-12,12))
		Goto See
	FireScatterAtk:
		TNT1 A 0 A_FaceTarget
		TNT1 AAAA 0 A_CustomMissile("IDMFatShot",26,random(-10,10),random(-10,10))
		HFEL F 6 Bright
		Goto See
	RevScatterAtk:
		TNT1 A 0 A_FaceTarget
		TNT1 AAAAAA 0 A_CustomMissile("IDMRevenantTracer",26,random(-10,10),random(-15,15))
		HFEL F 6 Bright
		Goto See
	Pain:
		HFEL G 6
		HFEL G 6 A_Pain
		Goto See
	Death:
		HFEL H 8 BRIGHT
		HFEL I 8 BRIGHT A_Scream
		HFEL JK 8 BRIGHT
		TNT1 AAAA 0 A_CustomMissile("IDMFatShot",32,0,random(0,359),2)
		TNT1 AAAA 0 A_CustomMissile("IDMRevenantTracer",32,0,random(0,359),2)
		HFEL L 1 BRIGHT
		TNT1 AAAA 0 A_CustomMissile("IDMFatShot",32,0,random(0,359),2)
		TNT1 AAAA 0 A_CustomMissile("IDMRevenantTracer",32,0,random(0,359),2)
		HFEL L 1 BRIGHT
		HFEL L 6 BRIGHT A_PainDie("IDMLostSoul")
		HFEL M 8 BRIGHT
		Stop
	}
}