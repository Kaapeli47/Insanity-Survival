/// Chaingun Major

Actor IDMChaingunMajor : IDMChaingunGuy
{
  Health 140
  GibHealth 100
  Painchance 40
  Mass 100
  Obituary "%o was turned into fine paste by the chaingun major."
  Decal Bulletchip
  +MISSILEMORE
  DamageType "Bullet"
  Tag "Chaingun Major"
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  Dropitem "SuperChaingun", 256
  DropItem "Clip", 16, 20
  DropItem "Shell", 32, 4
  DropItem "RocketAmmo", 32, 1
  DropItem "Cell", 8, 20
  States
  {
  Spawn:
    CPS2 AB 10 A_Look
    Loop
  See:
    CPS2 AABBCCDD 3 A_Chase
    Loop
  Missile:
	TNT1 A 0 A_Playsound("weapons/chngnstrt",CHAN_7)
    CPS2 E 12 A_FaceTarget
    CPS2 FEFEFEFE 4 Bright A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "IDMBulletPuff", 0, CBAF_NORANDOM)
	TNT1 A 0 A_Playsound("weapons/chngnloop",CHAN_7)
    CPS2 FE 2 Bright A_CustomBulletAttack(25, 0, 1, random(1,5)*3, "IDMBulletPuff", 0, CBAF_NORANDOM)
    CPS2 F 0 A_MonsterRefire(40,"MissileEnd")
    Goto Missile+10
  MissileEnd:
	CPS2 E 10 A_Playsound("weapons/chngnstop",CHAN_7)
	Goto See
  Pain:
    CPS2 G 2
    CPS2 G 2 A_Pain
    Goto See
  Death:
    CPS2 H 5
    CPS2 I 5 A_Scream
    CPS2 J 5 A_Fall
    CPS2 KLMNO 5
    CPS2 P -1 A_BossDeath
    Stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    CPS2 Q 5
    CPS2 R 5 A_XScream
    CPS2 S 5 A_Fall
    CPS2 TUVWX 5
    CPS2 Y -1
    Stop
  XDeath.Resurrector:
    CPS2 Q 5 
    CPS2 R 5 A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
    CPS2 S 5 A_Fall
    CPS2 TUVWX 5
    CPS2 Y -1
    Stop
  Raise:
    CPS2 P 5
    CPS2 ONMLKJIH 5
    Goto See
  }
}