/// Chainsaw Zombie

ACTOR IDMChainsawZombie
{
    Health 30
	GibHealth 30
    Radius 20
    Height 56
    Speed 10
    MeleeDamage 1
	MeleeRange 72
    PainChance 200
    MONSTER
    +FLOORCLIP 
    Obituary "%o was sliced in half by a chainsaw zombie."
    SeeSound "grunt/sight"
    MeleeSound "weapons/sawhit"
    PainSound "grunt/pain" 
    DeathSound "grunt/death" 
    ActiveSound "grunt/active"
	DamageType "Saw"
	Tag "Chainsaw Zombie"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "StartingChainsaw", 128
	DropItem "Clip", 16, 20
	DropItem "Shell", 16, 4
	DropItem "RocketAmmo", 16, 1
	DropItem "Cell", 8, 20
    States 
    { 
    Spawn: 
        CHAN AB 10 A_Look
        Loop 
    See: 
        CHAN A 2 A_Chase
        CHAN A 2 A_Chase
        CHAN A 2 A_Chase
        CHAN B 0 A_PlayWeaponSound("weapons/sawidle") 
        CHAN B 2 A_Chase
        CHAN B 2 A_Chase
        CHAN B 2 A_Chase
        CHAN C 0 A_PlayWeaponSound("weapons/sawidle") 
        CHAN C 2 A_Chase
        CHAN C 2 A_Chase
        CHAN C 2 A_Chase
        CHAN D 0 A_PlayWeaponSound("weapons/sawidle") 
        CHAN D 2 A_Chase
        CHAN D 2 A_Chase
        CHAN D 2 A_Chase
        CHAN A 0 A_PlayWeaponSound("weapons/sawidle") 
        Loop
    Melee: 
        CHAN E 1 A_FaceTarget
        CHAN F 1 A_MeleeAttack 
        Goto See 
    Pain: 
        CHAN G 2
        CHAN G 5 A_Pain 
        Goto See 
    Death: 
        CHAN H 5
        CHAN I 5 A_Scream 
        CHAN J 5 A_Fall 
        CHAN KLM 5 
        CHAN N -1 
        Stop 
    XDeath:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
        CHAN O 5
        CHAN P 5 A_XScream
        CHAN Q 5 A_Fall
        CHAN RSTUV 5
        CHAN W -1
        Stop
	XDeath.Resurrector:
        CHAN O 5
        CHAN P 5 A_XScream
		TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
		TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
        CHAN Q 5 A_Fall
        CHAN RSTUV 5
        CHAN W -1
        Stop
    Raise: 
        CHAN NMLKJIH 5
        Goto See 
    } 
}