/// Strogg Nailborg

ACTOR IDMNailborg
{
	Health 75
	GibHealth 75
	Mass 500
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	+MISSILEMORE
	Missiletype MonsterNail
	DamageType "Nail"
	SeeSound "strogg/sight"
	AttackSound "strogg/attack"
	PainSound "strogg/pain"
	DeathSound "strogg/death"
	ActiveSound "strogg/active"
	Tag "Strogg Nailborg"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "Nailgun", 64
	DropItem "Clip", 32, 20
	DropItem "Shell", 32, 4
	DropItem "RocketAmmo", 32, 1
	DropItem "Cell", 8, 20
	Obituary "%o got %hself nailed by a strogg nailborg."
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	States
	{
	Spawn:
		NLBG AB 10 A_Look
		Loop
	See:
		NLBG AABBCCDD 3 A_Chase
		Loop
	Missile:
		NLBG E 10 A_FaceTarget
		TNT1 A 0 A_Playsound("strogg/attack")
		NLBG F 2 Bright A_MissileAttack
		NLBG E 2 A_FaceTarget
		TNT1 A 0 A_Playsound("strogg/attack")
		NLBG F 2 Bright A_MissileAttack
		NLBG E 2 A_FaceTarget
		TNT1 A 0 A_Playsound("strogg/attack")
		NLBG F 2 Bright A_MissileAttack
		NLBG E 5
		Goto See
	ScrewAtk:
		TNT1 A 0 A_Playsound("weapons/screwgfr")
		TNT1 AAAAA 0 A_CustomMissile("MonsterScrew",32,0,random(-15,15))
		NLBG F 10 Bright A_CustomMissile("MonsterScrew",32,0,random(-15,15))
		NLBG E 10
		Goto See
	Pain:
		NLBG G 2
		NLBG G 2 A_Pain
		Goto See
	Death:
		NLBG H 5
		NLBG I 5 A_Scream
		NLBG J 5 A_NoBlocking
		NLBG K 5
		NLBG L 5
		NLBG M 5
		NLBG N -1
		Stop
	XDeath:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
		NLBG O 5
		NLBG P 5 A_XScream
		NLBG Q 5
		NLBG R 5 A_NoBlocking
		NLBG ST 5
		NLBG U -1
		Stop
	XDeath.Resurrector:
		NLBG O 5
		NLBG P 5 A_XScream
	    TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	    TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		NLBG Q 5
		NLBG RST 5 A_NoBlocking
		NLBG U -1
		Stop
	Raise:
		NLBG N 5
		NLBG MLKJIH 5
		Goto See
	}
}