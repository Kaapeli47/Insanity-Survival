/// Plasmagunner

ACTOR IDMPlasmagunGuy
{
    Health 60
	GibHealth 60
    Radius 20
    Height 56
    Speed 8
    PainChance 100
    MONSTER 
    +FLOORCLIP
    SeeSound "grunt/sight"
    AttackSound "grunt/attack"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    Obituary "%o was fried by a plasmagunner."
	DamageType "Plasma"
	Tag "Plasmagunner"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
    DropItem "IDMPlasmagun", 128
	DropItem "Clip", 32, 20
	DropItem "Shell", 32, 4
	DropItem "RocketAmmo", 32, 1
	DropItem "Cell", 8, 20
    MissileType MonsterPlasmaBall
    States
    {
    Spawn:
        ZMAY AB 10 A_Look
        Loop
    See:
        ZMAY AABBCCDD 4 A_Chase
        Loop
    Missile:
        ZMAY E 10 A_FaceTarget
        ZMAY F 4 Bright A_MissileAttack
		ZMAY E 0 A_CposRefire
        Goto Missile+1
    Pain:
        ZMAY G 2
        ZMAY G 2 A_Pain
        Goto See
    Death:
        ZMAY H 5
        ZMAY I 5 A_Scream
        ZMAY J 5 A_Fall
        ZMAY K 5
        ZMAY L -1
        Stop
    XDeath:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
        ZMAY O 5
        ZMAY P 5 A_XScream
        ZMAY Q 5 A_Fall
        ZMAY RSTU 5
        ZMAY V -1
        Stop
	XDeath.Resurrector:
        ZMAY O 5
        ZMAY P 5 A_XScream
		TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
		TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
        ZMAY Q 5 A_Fall
        ZMAY RSTU 5
        ZMAY V -1
        Stop
    Raise:
        ZMAY LKJIH 5
        Goto See
    }
}