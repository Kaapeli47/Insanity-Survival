/// Sergeant / Shotgun guy

ACTOR IDMShotgunGuy : ShotgunGuy
{
	Game Doom
	SpawnID 1
	Health 30
	GibHealth 30
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	Monster
	+MISSILEMORE
	+FLOORCLIP
	SeeSound "shotguy/sight"
	AttackSound "shotguy/attack"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"
	Obituary "%o was shot by a shotgunner."
	DamageType "Bullet"
	Tag "Shotgunner"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "IDMShotgun", 256
	DropItem "Clip", 32, 20
	DropItem "Shell", 16, 4
	DropItem "RocketAmmo", 32, 1
	DropItem "Cell", 8, 20
	States
	{
	Spawn:
		SPOS AB 10 A_Look
		Loop
	See:
		SPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		SPOS E 10 A_FaceTarget
		SPOS F 0 bright A_PlaySound("shotguy/attack", CHAN_WEAPON)
		SPOS F 10 bright A_CustomBulletAttack(22.5, 0, 3, random(1,5)*3, "IDMBulletPuff", 0, CBAF_NORANDOM)
		SPOS E 10
		Goto See
	Pain:
		SPOS G 2
		SPOS G 2 A_Pain
		Goto See
	Death:
		SPOS H 5
		SPOS I 5 A_Scream
		SPOS J 5 A_NoBlocking
		SPOS K 5
		SPOS L -1
		Stop
	XDeath:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
		SPOS M 5
		SPOS N 5 A_XScream
		SPOS O 5 A_NoBlocking
		SPOS PQRST 5
		SPOS U -1
		Stop
	XDeath.Resurrector:
		SPOS M 5
		SPOS N 5 A_XScream
		TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
		TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		SPOS O 5 A_NoBlocking
		SPOS PQRST 5
		SPOS U -1
		Stop
	Raise:
		SPOS L 5
		SPOS KJIH 5
		Goto See
	}
}