/// Strogg

ACTOR IDMStrogg
{
	Health 25
	GibHealth 25
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	+MISSILEMORE
	MissileType "MonsterNail"
	MissileHeight 32
	SeeSound "strogg/sight"
	AttackSound "strogg/attack"
	PainSound "strogg/pain"
	DeathSound "strogg/death"
	ActiveSound "strogg/active"
	Obituary "%o was nailed by a strogg."
	Tag "Strogg"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	DropItem "Clip", 32, 20
	DropItem "Shell", 32, 4
	DropItem "RocketAmmo", 32, 1
	DropItem "Cell", 8, 20
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	States
	{
	Spawn:
	  STRO AB 10 A_Look
	  Loop
	See:
	  STRO AABBCCDD 3 A_Chase
	  Loop
	Missile:
	  STRO E 6 A_FaceTarget
	  TNT1 A 0 A_Playsound("strogg/attack")
	  STRO F 5 Bright A_MissileAttack
	  STRO F 5
	  Goto See
	Pain:
	  STRO G 2
	  STRO G 2 A_Pain
	  Goto See
	Death:
	  STRO H 5
	  STRO I 5 A_Scream
	  STRO J 5 A_NoBlocking
	  STRO KLM 5
	  STRO N -1
	  Stop
	XDeath:
	  TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
	  STRO O 5
	  STRO P 5 A_XScream
	  STRO Q 5 A_NoBlocking
	  STRO RSTUV 5
	  STRO W -1
	  Stop
	XDeath.Resurrector:
	  STRO O 5
	  STRO P 5 A_XScream
	  TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	  TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	  STRO Q 5 A_NoBlocking
	  STRO RSTUV 5
	  STRO W -1
	  Stop
	Raise:
	  STRO M 5
	  STRO LKJIH 5
	  Goto See
	}
}
	  
	  
