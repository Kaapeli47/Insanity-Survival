/// Strogg Chaingunner

ACTOR IDMStroggChaingunGuy : IDMChaingunGuy
{
	Health 120
	GibHealth 100
	Mass 500
	Speed 10
	SeeSound "strenf/sight"
	PainSound "strenf/pain"
	DeathSound "strenf/death"
	ActiveSound "strenf/active"
	AttackSound "weapons/chngun"
	Tag "Strogg Chaingunner"
	Obituary "%o was serrated by a strogg chaingunner."
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	States
	{
	Spawn:
		STCG AB 10 A_Look
		Loop
	See:
		STCG AABBCCDD 3 A_Chase
		Loop
	Missile:
		STCG E 8 A_FaceTarget
		STCG F 4 BRIGHT A_CustomBulletAttack(20, 0, 1, random(1,5)*3, "IDMBulletPuff", 0, CBAF_NORANDOM)
		STCG E 4 BRIGHT A_CustomBulletAttack(20, 0, 1, random(1,5)*3, "IDMBulletPuff", 0, CBAF_NORANDOM)
		STCG F 1 Bright A_CPosRefire
		Goto Missile+1
	Pain:
		STCG G 2
		STCG G 2 A_Pain
		Goto See
	Death:
		STCG H 5
		STCG I 5 A_Scream
		STCG J 5 A_NoBlocking
		STCG KLM 5
		STCG N -1
		Stop
	XDeath:
		TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
		STCG O 5 
		STCG P 5 A_XScream
		STCG Q 5 A_NoBlocking
		STCG RS 5
		STCG T -1
		Stop
	XDeath.Resurrector:
		STCG O 5 
		STCG P 5 A_XScream
	    TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	    TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
		STCG Q 5 A_NoBlocking
		STCG RS 5
		STCG T -1
		Stop
	Raise:
		STCG N 5
		STCG MLKJIH 5
		Goto See
	}
}