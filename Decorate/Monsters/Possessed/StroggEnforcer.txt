/// Strogg Enforcer

ACTOR IDMStroggEnforcer
{
	Health 120
	GibHealth 100
	Radius 20
	Height 56
	Mass 500
	Speed 8
	PainChance 170
	Monster
	+FLOORCLIP
	SeeSound "strenf/sight"
	PainSound "strenf/pain"
	DeathSound "strenf/death"
	ActiveSound "strenf/active"
	AttackSound "weapons/chngun"
	Obituary "%o was filled with nails by a strogg enforcer."
	Tag "Strogg Enforcer"
	DropItem "IDMHealthBonusPickup", 64
	DropItem "IDMArmorBonusPickup", 64
	Dropitem "Nailgun", 256
	DropItem "Clip", 16, 20
	DropItem "Shell", 32, 4
	DropItem "RocketAmmo", 32, 1
	DropItem "Cell", 8, 20
	DamageFactor "Rocket", 0.75
	DamageFactor "Grenade", 0.75
	DamageFactor "Fire", 0.75
	DamageFactor "PlayerRocket", 0.75
	DamageFactor "PlayerGrenade", 0.75
	States
	{
	Spawn:
	  STGE AB 10 A_Look
	  Loop
	See:
	  STGE AABBCCDD 4 A_Chase
	  Loop
	Missile:
	  STGE E 10 A_FaceTarget
	  TNT1 A 0 A_Playsound("strogg/attack")
	  STGE F 3 Bright A_CustomMissile("MonsterNail",32,12,random(-12,12))
	  TNT1 A 0 A_FaceTarget
	  STGE E 3 Bright
	  TNT1 A 0 A_CPosRefire
	  Goto Missile+1
	Pain:
	  STGE G 2
	  STGE G 2 A_Pain
	  Goto See
	Death:
	  STGE H 6
	  STGE I 6 A_Scream
	  STGE J 6 A_NoBlocking
	  STGE KLM 6
	  STGE N -1
	  Stop
	XDeath:
	  TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
	XDeath.Massacre:
	  STGE O 6
	  STGE P 6 A_XScream
	  STGE Q 6 A_NoBlocking
	  STGE RS 6
	  STGE T -1
	  Stop
	XDeath.Resurrector:
	  STGE O 6
	  STGE P 6 A_XScream
	  TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
	  TNT1 A 0 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	  STGE Q 6 A_NoBlocking
	  STGE RS 6
	  STGE T -1
	  Stop
	Raise:
	  STGE NMLKJIH 5
	  Goto See
	}
}