/// Common projectiles

ACTOR MonsterNail : Nail
{
	Speed 60
	Damage (5 * random(1,4))
}

ACTOR MonsterBigNail : MonsterNail
{
	Speed 60
	Damage (5 * random(1,6))
}

ACTOR MonsterScrew : Screw
{
	Speed 60
	Damage (5 * random(1,3))
}

ACTOR MonsterRocket : IDMRocket
{
	Speed 20
	Damage 20
	DamageType "Rocket"
	States
	{
	XDeath:
	Death:
      MISL B 6 Bright A_Explode(128,128,0)
      MISL C 5 Bright
	  MISL D 4 Bright
      Stop
	}
}

ACTOR MonsterGrenade : IDMGrenade
{
	Speed 25
	Damage 20
	DamageType "Grenade"
	States
	{
	XDeath:
	Death:
      MISL B 6 Bright A_Explode(128,128,0)
      MISL C 5 Bright
	  MISL D 4 Bright
      Stop
	}
}

ACTOR MonsterGRifleGrenade : GRifleGrenade
{
	Speed 40
	Damage 5
	DamageType "Grenade"
	Gravity 0.2
	States
	{
	XDeath:
	Death:
	  TNT1 A 0
	  TNT1 A 0 A_ScaleVelocity(0)
	  TNT1 A 0 A_ChangeFlag(NOGRAVITY,true)
	  TNT1 A 0 A_Scream
	  TNT1 A 0 A_SetScale(0.7825)
      MISL B 6 Bright A_Explode(100,100,0,0,48)
      MISL C 5 Bright
      MISL D 4 Bright
      Stop
	}
}

ACTOR MonsterSeekerMissile : HeatSeekerMissile
{
	Damage 10
	Speed 15
	DamageType "Rocket"
}

ACTOR MonsterSuperSeekerMissile : HeatSeekerMissile2
{
	Damage 10
	Speed 15
	Damagetype "Rocket"
}

ACTOR MonsterMiniMissile : IDMMiniMissile
{
	Damage (5 * random(1,4))
	Speed 20
	DamageType "Rocket"
	States
	{
	Death:
	  TNT1 A 0
      MISL B 6 Bright A_Explode(48,80,0)
      MISL C 5 Bright
	  MISL D 4 Bright
      Stop
	}
}

ACTOR MonsterPlasmaBall : IDMPlasmaBall
{
	Damage (1 * random(1,40))
	Speed 25
}


ACTOR InsanityPlasmaBall : MonsterPlasmaBall
{
    Damage (2 * random(1,25))
	DamageType "RedPlasma"
	Decal PlasmaScorchLower
	Speed 40
    States
    {
    Spawn:
        IPLS ABABABAB 2 Bright
        TNT1 A 0 A_VileStart
        Loop
    Death:
        IPLE A 4 Bright A_Playsound("weapons/plasmax",0,0.25)
		IPLE BCDE 3 Bright
        Stop
    }
}

ACTOR MonsterIonBall : IonBall
{
	Damage 3
	Speed 45
	States
	{
	Death:
	  TNT1 A 0
	  IONB B 1 Bright A_Explode(25,96,0)
	  IONB B 2 Bright A_Playsound("weapons/ionblx",CHAN_VOICE)
	  IONB CDEFGHIJKL 3 Bright
	  Stop
	}
}


ACTOR MonsterLaserShot : FastProjectile
{
	Radius 5
	Height 5
	Scale 0.65
	Speed 60
	Damage (1 * random(1,16))
	Renderstyle Translucent
	MissileType "MonsterLaserTrail"
	MissileHeight 8
	+RANDOMIZE
	+SKYEXPLODE
	Seesound "monster/laserfr"
	Deathsound "monster/laserx"
	DamageType "RedPlasma"
	States
	{
	Spawn:
		ILAS A 1 Bright
		Loop
	Death:
	    ILAS A 2 Bright A_SetScale(1.2)
	    ILAS A 2 Bright A_SetScale(0.9)
	    ILAS A 2 Bright A_SetScale(0.6)
	    ILAS A 2 Bright A_SetScale(0.3)
		Stop
	}
}

ACTOR MonsterLaserTrail
{
	Scale 0.65
	+NOBLOCKMAP
	+NOGRAVITY
	+SHADOW
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	+CLIENTSIDEONLY
	RenderStyle Translucent
	Alpha 0.8
	States
	{
	Spawn:
	  TNT1 A 1
	  ILAS A 1 Bright A_FadeOut(0.2)
	  Stop
    }
}

ACTOR MonsterUnmakerRay : UnmakerRay
{
	MissileType "MonsterUnmakerTrail"
	Damage (25 * random(1,3))
	Speed 250
	-RIPPER
	States
	{
	Spawn:
	  TNT1 A 0
	  TNT1 A 0
	SpawnLoop:
 	  IPLS AB 4 Bright
	  Loop
	Death:
	  TNT1 A 0
	  TNT1 A 0 A_Explode(32,48,0)
	  IPLE ABCDE 4 Bright
	  Stop
    }
}

ACTOR MonsterUnmakerTrail : UnmakerTrail
{
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_Playsound("weapons/unmakerfly",CHAN_AUTO,0.1)
    IPLS AAAAA 4 Bright A_Fadeout(0.1)
	PLS2 AAAAA 4 Bright A_Fadeout(0.1)
    Stop
  }
}

ACTOR MonsterBFGBall : IDMBFGBall
{
  Speed 20
  Damage 100
  ProjectileKickBack 100
  -RIPPER
  States
  {
  Spawn:
    BFS1 AB 2 Bright
    Loop
  Death:
    BFE1 A 8 Bright
	TNT1 A 0 A_Explode(128,160,0)
	BFE1 B 8 Bright A_BFGSpray("IDMBFGExtra",40,6)
	BFE1 CDEF 8 Bright
	Stop
  }
}

ACTOR MonsterFTFire : FTFire
{
  Speed 30
  Damage (1 * random(1,4))
  States
  {
  Spawn:
    TNT1 A 2 A_Explode(1*random(1,4),8,0,0,4)
    FRFX ABCD 2 bright A_Explode(1*random(1,4),8,0,0,4)
    TNT1 A 0 A_LowGravity
    FRFX EFG 2 bright A_Explode(1*random(1,4),16,0,0,8)
    FRFX HIJ 2 bright A_Explode(1*random(1,4),32,0,0,16)
    FRFX KLM 2 bright A_Explode(1*random(1,4),64,0,0,16)
    FRFX NO 2 bright
    Stop
  Death:
    FRFX HIJ 2 bright A_Explode(1*random(1,4),32,0,0,16)
    FRFX KLM 2 bright A_Explode(1*random(1,4),64,0,0,16)
    FRFX NO 2 bright
    Stop
  }
}