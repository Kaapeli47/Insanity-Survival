/// Weredragon

ACTOR IDMBeast
{
  Scale 0.9
  Health 400
  GibHealth 100
  Radius 20
  Height 56
  Mass 400
  Speed 16
  Painchance 100
  Monster
  Species "Revenant"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +FLOORCLIP
  SeeSound "beast/sight"
  AttackSound "beast/attack"
  PainSound "beast/pain"
  DeathSound "beast/death"
  ActiveSound "beast/active"
  Obituary "$OB_BEAST"
  Tag "Weredragon"
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  DropItem "Clip", 64, 20
  DropItem "ClipBox", 8, 50
  DropItem "Shell", 64, 4
  DropItem "ShellBox", 8, 10
  DropItem "RocketAmmo", 64, 1
  DropItem "RocketBox", 8, 5
  DropItem "Cell", 32, 20
  DropItem "Cellpack", 4, 100
  DropItem "IDMMana", 4
  DropItem "IDMMana", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMGas", 4
  DropItem "IDMGas", 4
  States
  {
  Spawn:
    BEAS AB 10 A_Look
    Loop
  See:
    BEAS ABCDEF 3 A_Chase
    Loop
  Missile:
	TNT1 A 0 A_Jump(128,"BurstAtk")
  Melee:
    BEAS H 10 A_FaceTarget
	TNT1 A 0 A_FaceTarget
    BEAS I 10 Bright A_CustomComboAttack("IDMBeastBall", 44, random[BeastAttack](2,16)*3, "beast/attack")
    Goto See
  BurstAtk:
    BEAS H 10 A_FaceTarget
	TNT1 A 0 A_FaceTarget
    BEAS I 6 Bright A_CustomMissile("IDMBeastBall", 44)
	TNT1 A 0 A_FaceTarget
    BEAS I 6 Bright A_CustomMissile("IDMBeastBall", 44)
	TNT1 A 0 A_FaceTarget
    BEAS I 6 Bright A_CustomMissile("IDMBeastBall", 44)
    Goto See
  Pain:
    BEAS G 3
    BEAS G 3 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
    BEAS R 6
    BEAS S 6 A_Scream
    BEAS TUV 6
    BEAS W 6 A_NoBlocking
    BEAS XY 6
    BEAS Z -1
    Stop
  XDeath:
	TNT1 A 0 A_Jump(1,"XDeath.Resurrector")
  XDeath.Massacre:
    BEAS J 5
    BEAS K 6 A_XScream
    BEAS L 5
    BEAS M 6
    BEAS N 5
    BEAS O 6 A_NoBlocking
    BEAS P 5
    BEAS Q -1
    Stop
  XDeath.Resurrector:
    BEAS J 5
    BEAS K 6 A_XScream
	TNT1 A 0 A_SpawnItemEx("IDMTeleportFog")
    BEAS L 5 A_SpawnItemEx("IDMResurrector",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
    BEAS M 6
    BEAS N 5
    BEAS O 6 A_NoBlocking
    BEAS P 5
    BEAS Q -1
    Stop
  Raise:
    BEAS YXWVUTSR 5
	Goto See
  }
}

ACTOR IDMBeastBall : BeastBall
{
  Radius 9
  Height 8
  Speed 24
  FastSpeed 30
  Damage 10
  Projectile
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  -NOBLOCKMAP
  +WINDTHRUST
  +SPAWNSOUNDSOURCE
  RenderStyle Add
  SeeSound "beast/attack"
  DeathSound "hknight/hit"
  DamageType "Fire"
  States
  {
  Spawn:
    FRB1 AABBCC 2 A_SpawnItemEx("Puffy", random2[BeastPuff]()*0.015625, random2[BeastPuff]()*0.015625, random2[BeastPuff]()*0.015625, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION, 64)
    Loop
  Death:
    FRB1 DEFGH 4
    Stop
  }
}