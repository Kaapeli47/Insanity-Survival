ACTOR IDMDeathIncarnate
{
	Health 300
	GibHealth 100
	Radius 20
	Height 56
	Mass 500
	Speed 12
	MeleeDamage 7
	PainChance 64
	MONSTER
	+FLOORCLIP
	+SHORTMISSILERANGE
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+DONTHURTSPECIES
	Species "Revenant"
	Tag "Death Incarnate"
	Obituary "%o was killed by a death incarnate's glare."
	HitObituary "%o was beaten by a death incarnate."
	SeeSound "monster/incsit"
	PainSound "skeleton/pain"
	DeathSound "monster/incdth"
	ActiveSound "monster/incact"
	AttackSound "monster/incatk"
	MeleeSound "monster/inchit"
	States
	{
	Spawn:
		INCA AB 10 A_Look
		Loop
	See:
		INCA AABBCCDDEEFF 2 A_Chase
		Loop
	Melee:
		INCA G 0 A_Jump(128,"Melee2")
		INCA G 0 A_FaceTarget
		INCA G 6 A_SkelWhoosh
		INCA H 6 A_FaceTarget
		INCA I 6 A_MeleeAttack
		Goto See
	Melee2:
		INCA R 0 A_FaceTarget
		INCA R 5 A_SkelWhoosh
		INCA S 5 A_FaceTarget
		INCA T 5 A_MeleeAttack
		INCA G 0 A_FaceTarget
		INCA G 5 A_SkelWhoosh
		INCA H 5 A_FaceTarget
		INCA I 5 A_MeleeAttack
		Goto See
	Missile:
		INCA JJ 5 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Jump(128,"TracerAtk")
		INCA UUUUU 1 Bright A_CustomMissile("MonsterLaserShot",65,random(-5,-2),random(-2,2))
		TNT1 A 0 A_FaceTarget
		INCA UUUUU 1 Bright A_CustomMissile("MonsterLaserShot",65,random(-5,-2),random(-2,2))
		TNT1 A 0 A_FaceTarget
		INCA UUUUU 1 Bright A_CustomMissile("MonsterLaserShot",65,random(-5,-2),random(-2,2))
		INCA K 10 A_FaceTarget
		Goto See
	TracerAtk:
		INCA U 6 Bright A_CustomMissile("IDMDeathIncarnateTracer",65)
		TNT1 A 0 A_FaceTarget
		INCA U 6 Bright A_CustomMissile("IDMDeathIncarnateTracer",65)
		TNT1 A 0 A_FaceTarget
		INCA U 6 Bright A_CustomMissile("IDMDeathIncarnateTracer",65)
		INCA K 10 A_FaceTarget
		Goto See
	Pain:
		INCA L 5
		INCA L 5 A_Pain
		Goto See
	Death:
		INCA L 0 A_Jump(192,7)
		INCA LM 7
		INCA N 7 A_Scream
		INCA O 7 A_NoBlocking
		INCA P 7
		INCA Q -1
		Stop
		INCA LM 7
		INCA N 7 A_Scream
		INCA O 7 A_NoBlocking
		INCA P 7
		INCA Q 0 A_Jump(192,4)
		INCA Q 0 A_Jump(128,4)
		INCA Q 0 A_Jump(96,4)
		INCA Q 0 A_Jump(64,4)
		INCA Q 115
		INCA Q 115
		INCA Q 115
		INCA Q 115
		INCA Q 115
		INCA Q -1 Thing_Raise(0)
		Stop
	XDeath:
		INCX A 10 Bright A_Playsound("monster/incexp")
		INCX BC 5 Bright
		INCX D 5 Bright A_NoBlocking
		INCX D 0 Bright A_SpawnItem("IDMHadesSphere",0,32)
		INCX EFGHIJ 5 Bright
		INCX K -1
	Raise:
		INCA Q 0 A_KillChildren
		INCA QPONML 5
		Goto See
	}
}

ACTOR IDMDeathIncarnateTracer : IDMRevenantTracer
{
	Speed 15
	Damage 10
	DamageType "RedPlasma"
	+SEEKERMISSILE
	States
	{
	Spawn:
	  AHB2 BAB 2 Bright
	  TNT1 A 0 A_Jump(128,"SpawnLoop2")
	SpawnLoop:
	  TNT1 A 0 A_SeekerMissile(2,10)
	  AHB2 AA 1 Bright A_Weave(3,0,4,0)
	  TNT1 A 0 A_SpawnItemEx("IDMArchHealPuff")
	  TNT1 A 0 A_Seekermissile(2,10)
	  AHB2 BB 1 Bright A_Weave(3,0,4,0)
	  TNT1 A 0 A_SpawnItemEx("IDMArchHealPuff")
	  Loop
	SpawnLoop2:
	  TNT1 A 0 A_SeekerMissile(2,10)
	  AHB2 AA 1 Bright A_Weave(3,0,-4,0)
	  TNT1 A 0 A_SpawnItemEx("IDMArchHealPuff")
	  TNT1 A 0 A_Seekermissile(2,10)
	  AHB2 BB 1 Bright A_Weave(3,0,-4,0)
	  TNT1 A 0 A_SpawnItemEx("IDMArchHealPuff")
	  Loop
	Death:
	  AHB2 C 6 Bright
	  AHB2 D 5 Bright
	  AHB2 E 4 Bright
	  Stop
	}
}