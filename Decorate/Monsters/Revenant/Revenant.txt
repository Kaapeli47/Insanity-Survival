/// Revenant

ACTOR IDMRevenant : Revenant
{
  Health 300
  Radius 20
  Height 56
  Mass 1000
  Speed 10
  PainChance 100
  Monster
  MeleeThreshold 196
  Scale 0.95
  Species "Revenant"
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +MISSILEMORE
  +QUICKTORETALIATE
  +FLOORCLIP
  SeeSound "skeleton/sight"
  PainSound "skeleton/pain"
  DeathSound "skeleton/death"
  ActiveSound "skeleton/active"
  MeleeSound "skeleton/melee"
  HitObituary "%o was punched by a revenant."
  Obituary "%o couldn't evade a revenant's fireball."
  Tag "Revenant"
  DropItem "IDMHealthBonusPickup", 64
  DropItem "IDMArmorBonusPickup", 64
  DropItem "Clip", 64, 20
  DropItem "ClipBox", 8, 50
  DropItem "Shell", 64, 4
  DropItem "ShellBox", 8, 10
  DropItem "RocketAmmo", 64, 1
  DropItem "RocketBox", 8, 5
  DropItem "Cell", 32, 20
  DropItem "Cellpack", 4, 100
  DropItem "IDMMana", 4
  DropItem "IDMMana", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMMaceAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMCatAmmo", 4
  DropItem "IDMGas", 4
  DropItem "IDMGas", 4
  States
  {
  Spawn:
    SKEL AB 10 A_Look
    Loop
  See:
    SKEL AABBCCDDEEFF 2 A_Chase
    Loop
  Melee:
    SKEL G 1 A_FaceTarget
    SKEL G 6 A_SkelWhoosh
    SKEL H 6 A_FaceTarget
    SKEL I 6 A_CustomMeleeAttack(random(1,10)*6,"skeleton/melee",none,"Punch",true)
    Goto See
  Missile:
    SKEL J 10 Bright A_FaceTarget
	TNT1 A 0 A_Jump(192,"MultiMissile","RapidMissile","RapidMultiMissile")
	TNT1 A 0 A_Jump(128,"Missile2")
    TNT1 A 0 A_FaceTarget
    SKEL K 10 A_CustomMissile("IDMRevenantTracer",56,-12)
    SKEL K 10 A_FaceTarget
    Goto See
  Missile2:
    TNT1 A 0 A_FaceTarget
    SKEL K 10 A_CustomMissile("IDMRevenantTracer",56,12)
    SKEL K 10 A_FaceTarget
    Goto See
  MultiMissile:
    TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("IDMRevenantTracer",56,-12)
    SKEL K 10 A_CustomMissile("IDMRevenantTracer",56,12)
    SKEL K 10 A_FaceTarget
    Goto See
  RapidMissile:
    TNT1 A 0 A_FaceTarget
    SKEL J 10 Bright A_CustomMissile("IDMRevenantTracer",56,-12)
    TNT1 A 0 A_FaceTarget
    SKEL J 5 Bright A_CustomMissile("IDMRevenantTracer",56,12)
    TNT1 A 0 A_FaceTarget
    SKEL K 10 A_FaceTarget
    Goto See
  RapidMultiMissile:
    TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("IDMRevenantTracer",56,-12)
    SKEL J 10 Bright A_CustomMissile("IDMRevenantTracer",56,12)
    TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("IDMRevenantTracer",56,-12)
    SKEL J 5 Bright A_CustomMissile("IDMRevenantTracer",56,12)
    TNT1 A 0 A_FaceTarget
    SKEL K 10 A_FaceTarget
    Goto See
  Pain:
    SKEL L 5
    SKEL L 5 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_Jump(1,"Death.Doppel")
  Death.Massacre:
    SKEL LM 7
    SKEL N 7 A_Scream
    SKEL O 7 A_NoBlocking
    SKEL P 7
    SKEL Q -1
    Stop
  Death.Doppel:
	SKEL M 1 A_Scream
	SKEL MMMMMMMMM 1 A_FadeOut(0.05)
	TNT1 A 0 A_Playsound("doppel/morph2",CHAN_BODY,1,0,0.5)
	DOP2 T 2 Bright A_SetTranslucent(0.25)
	DOP2 SRQPO 2 Bright
	DOP2 NMLKJIHGFEDCB 2 Bright A_Fadein(0.05)
	TNT1 A 0 A_NoBlocking
	DOP2 A 1 A_SpawnItemEx("IDMDoppelganger",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION)
	Stop
  Raise:
    SKEL Q 5
    SKEL PONML 5
    Goto See
  }
}

// Revenant Tracer

ACTOR IDMRevenantTracer : RevenantTracer replaces RevenantTracer
{
	Game Doom
	SpawnID 53
	Radius 11
	Height 8
	Speed 10
	Damage 10
	Projectile 
	+SEEKERMISSILE 
	+RANDOMIZE
	SeeSound "skeleton/attack"
	DeathSound "skeleton/tracex"
	RenderStyle Add
	DamageType "Fire"
	States
	{
	Spawn:
		FATB AB 2 BRIGHT A_Tracer
		Loop
	Death:
		FBXP A 6 BRIGHT
		FBXP B 5 BRIGHT
		FBXP C 4 BRIGHT
		Stop
	}
}