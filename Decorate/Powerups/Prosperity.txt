// Prosperity

ACTOR ProsperitySphere : CustomInventory 10081
{
    +FLOAT
    +FLOATBOB
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+COUNTITEM
	-INVENTORY.INVBAR
	Inventory.PickupMessage "Prosperity!"
	Inventory.PickupSound "misc/p_pkup"
	Inventory.PickupAnnouncerEntry "prosperity"
	Inventory.RespawnTics 4200
    Inventory.MaxAmount 0
	Tag "Prosperity Sphere"
    //$Category Powerups
	States
	{
	Spawn:
	  AMSP ABCD 6 Bright
	  Loop
	Pickup:
	  TNT1 A 0 A_JumpIfInventory("PowerRageInvulnerable",1,"NoProt")
	  TNT1 A 0 A_GiveInventory("PickupProtection")
    NoProt:
	  TNT1 A 0 A_Print("\n\n\n\n\n\nProsperity!\n\n\n\n200 Health, random Light Armor & full ammo!\nInfinite ammo for 20 seconds!",4)
	  TNT1 A 0 Healthing(200,200)
	  TNT1 A 0 A_GiveInventory("IDMPowerupTrail10")
	  TNT1 A 0 A_GiveInventory("ProsperityInfiniteAmmo")
	  TNT1 A 0 A_GiveInventory("Clip", 200)
	  TNT1 A 0 A_GiveInventory("Shell", 40)
	  TNT1 A 0 A_GiveInventory("RocketAmmo", 30)
	  TNT1 A 0 A_GiveInventory("Cell", 300)
	  TNT1 A 0 A_Jump(256,"BulletArmor","FragArmor","TechArmor")
	  Stop
	BulletArmor:
	  TNT1 A 0 A_GiveInventory("BulletArmor")
	  Stop
	FragArmor:
	  TNT1 A 0 A_GiveInventory("FragArmor")
	  Stop
	TechArmor:
	  TNT1 A 0 A_GiveInventory("TechArmor")
	  Stop
	}
}

ACTOR PowerIDMInfiniteAmmo : PowerInfiniteAmmo
{
	Inventory.Icon "PROSSP1"
}

ACTOR ProsperityInfiniteAmmo : PowerupGiver
{
	Game Doom
	Powerup.Duration -30
	Powerup.Type "IDMInfiniteAmmo"
	Powerup.Color "Coral", 0.2
	Inventory.MaxAmount 0
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	-INVENTORY.INVBAR
}