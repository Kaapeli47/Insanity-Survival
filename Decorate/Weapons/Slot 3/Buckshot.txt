// Buckshot (Quake)

ACTOR BuckShot : IDMWeapon 10007
{
	Weapon.SelectionOrder 800
	Weapon.AmmoType "Shell"
	Weapon.AmmoGive 4
	Weapon.AmmoUse 2
	Weapon.Kickback 200
	Inventory.PickupMessage "Buckshot! (Slot 3)"
	Obituary "%o ate 2 loads of %k's Buckshot."
	Attacksound "weapons/dbstck"
	Tag "Buckshot"
	//$Category Weapons
	Decal Bulletchip
	Damagetype "Bullet"
	+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	+WEAPON.NOLMS
	States
	{
	Spawn:
	  DBMS A -1 Bright
	  Stop
	Ready:
	  DBST A 1 A_WeaponReady
	  Loop
	Deselect:
	  TNT1 A 0 A_Lower
	  TNT1 A 0 A_Lower
	  DBST A 1 A_Lower
	  Loop
	Select:
	  TNT1 A 0 A_PlaySound("weapons/wswitch",CHAN_WEAPON)
	Sloop:
	  TNT1 A 0 A_Raise
	  TNT1 A 0 A_Raise
	  DBST A 1 A_Raise
	  Loop
	Fire:
	  TNT1 A 0 A_GunFlash
	  TNT1 A 0 A_JumpIfInventory("PowerIDMWeaponLevel2",1,"Level2")
	  TNT1 A 0 A_JumpIfInventory("PowerQuadDamage",1,"Quad")
	Normal:
	  DBST A 4 A_Firebullets(7.0,3.55,18,5,"IDMShotPuff")
	  Goto FireEnd
	Level2:
	  TNT1 A 0 A_JumpIfInventory("PowerQuadDamage", 1, "QuadLevel2")
	  DBST A 2 A_Firebullets(7.0,3.55,18,5,"IDMShotPuff",FBF_NOFLASH)
	  DBST A 2 A_Firebullets(7.0,3.55,18,5,"IDMShotPuff")
	  Goto FireEnd
	Quad:
	  TNT1 A 0 A_Playsound("misc/qdmg",CHAN_AUTO,1)
	  DBST A 4 A_Firebullets(7.0,3.55,18,5,"ExtremeShotPuff")
	  Goto FireEnd
	QuadLevel2:
	  TNT1 A 0 A_Playsound("misc/qdmg2",CHAN_5,1)
	  DBST A 2 A_Firebullets(7.0,3.55,18,5,"ExtremeShotPuff",FBF_NOFLASH)
	  TNT1 A 0 A_Playsound("misc/qdmg2",CHAN_5,1)
	  DBST A 2 A_Firebullets(7.0,3.55,18,5,"ExtremeShotPuff")
	FireEnd:
	  DBST B 8
	  DBST C 7
	  DBST A 6
	  TNT1 A 0 A_Refire
	  Goto Ready
	Flash:
	  DBST D 4 Bright A_Light1
	  Goto LightDone
	}
}