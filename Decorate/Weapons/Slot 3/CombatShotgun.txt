// Combat Shotgun (Doom)

ACTOR IDMShotgun : IDMWeapon 10008
{
	Weapon.SelectionOrder 600
	Weapon.Sisterweapon "ShotgunLevel2"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.Kickback 200
	Weapon.AmmoType "Shell"
	Inventory.PickupMessage "Combat Shotgun! (Slot 3)"
	Obituary "%o was peppered by %k's Combat Shotgun."
	Tag "Combat Shotgun"
	//$Category Weapons
	Decal BulletChip
	Damagetype "Bullet"
	AttackSound "weapons/shotgf"
	States
	{
	Ready:
	  SHTG A 1 A_WeaponReady
      Loop
	Deselect:
      TNT1 A 0 A_Lower
      TNT1 A 0 A_Lower
      SHTG A 1 A_Lower
      Loop
	Select:
	  TNT1 A 0 A_PlaySound("weapons/wswitch",CHAN_WEAPON)
	Sloop:
      TNT1 A 0 A_Raise
      TNT1 A 0 A_Raise
      SHTG A 1 A_Raise
      Loop
	Fire:
	  TNT1 A 0 A_JumpIfInventory("PowerQuadDamage", 1, "Quad")
	Normal:
      TNT1 A 0 A_FireBullets (5.6, 1.775, 21, 5, "IDMShotPuff")
	  Goto FireEnd
	Quad:
	  TNT1 A 0 A_Playsound("misc/qdmg",CHAN_AUTO,1)
	  TNT1 A 0 A_FireBullets (5.6, 1.775, 21, 5, "ExtremeShotPuff")
	FireEnd:
      SHTG A 4 Offset(0,38) A_GunFlash
      SHTG A 2 Offset(0,35)
      SHTG A 1 Offset(0,33)
      SHTG BC 5
      SHTG D 4
      SHTG CB 5
      SHTG A 4
      SHTG A 7 A_ReFire
	  Goto Ready
	Flash:
      SHTF B 4 Bright A_Light1
      SHTF A 2 Bright A_Light2
      Goto LightDone
	Spawn:
      SHOT A -1 Bright
      Stop
	}
}

ACTOR ShotgunLevel2 : IDMShotgun
{
	+POWERED_UP
	+WEAPON.CHEATNOTWEAPON
	Weapon.Sisterweapon "IDMShotgun"
	Damagetype "Bullet"
	Tag "Shotgun"
	AttackSound "weapons/shotgf2"
	States
	{
	Ready:
      SHL2 A 1 A_WeaponReady
    Loop
    Deselect:
	  SHL2 A 0 A_Lower
      SHL2 A 0 A_Lower
      SHL2 A 1 A_Lower
      Loop
	Select:
	  TNT1 A 0 A_PlaySound("weapons/wswitch",CHAN_WEAPON)
	Sloop:
      SHL2 A 0 A_Raise
      SHL2 A 0 A_Raise
      SHL2 A 1 A_Raise
      Loop
	Fire:
      SHL2 A 1
	  SHL2 A 0 A_JumpIfInventory("PowerQuadDamage", 1, "Quad")
    Normal:
      SHL2 A 0 A_FireBullets (5.6, 3.55, 42, 5, "IDMShotPuff")
	  Goto FireEnd
    Quad:
	  TNT1 A 0 A_Playsound("misc/qdmg",CHAN_AUTO,1)
	  SHL2 A 0 A_FireBullets (5.6, 3.55, 42, 5, "ExtremeShotPuff")
	FireEnd:
      SHL2 A 4 Offset(0,38) A_GunFlash
      SHL2 A 2 Offset(0,35)
      SHL2 A 1 Offset(0,33)
      SHL2 BC 5
      SHL2 D 4
      SHL2 CB 5
      SHL2 A 3
      SHL2 A 7 A_ReFire
      Goto Ready
	Flash:
      SHL2 F 4 Bright A_Light1
      SHL2 E 2 Bright A_Light2
      Goto LightDone
	}
}