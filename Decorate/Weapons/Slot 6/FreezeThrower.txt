// Freeze Thrower

ACTOR FreezeThrower : IDMWeapon 10034
{
  Weapon.SelectionOrder 50
  Weapon.AmmoUse 3
  Weapon.AmmoGive 100
  Weapon.AmmoType "Cell"
  Inventory.PickupMessage "Freezethrower! (Slot 6)"
  Tag "Freezethrower"
  //$Category Weapons
  +INVENTORY.INTERHUBSTRIP
  +WEAPON.BFG
  +WEAPON.NOLMS
  States
  {
  Ready:
    FRSG A 1 A_WeaponReady
    Loop
  Deselect:
    FRSG A 1 A_Lower
    Loop
  Select:
	TNT1 A 0 A_PlaySound("weapons/wswitch",CHAN_WEAPON)
  Sloop:
    FRSG A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_Playsound("weapons/freezecnnf",CHAN_WEAPON)
	FRSG A 18 A_GunFlash
	Goto Hold
  Hold:
    TNT1 A 0 A_GunFlash("Flash2")
	TNT1 A 0 A_JumpIfInventory("PowerIDMWeaponLevel2",1,"Level2")
	TNT1 A 0 A_JumpIfInventory("PowerQuadDamage",1,"Quad")
	FRSG A 2 Bright A_FireCustomMissile("FreezeBall",frandom(-6,6),1,0,0,0)
    TNT1 A 0 A_GunFlash("Flash3")
	FRSG A 2 Bright A_FireCustomMissile("FreezeBall",frandom(-6,6),1,0,0,0)
  FireEnd:
	TNT1 A 0 A_Refire
    FRSG B 30 A_GunFlash("Flash4")
    Goto Ready
  Quad:
    TNT1 A 0 A_Playsound("misc/qdmg2",CHAN_5,1)
	FRSG A 2 Bright A_FireCustomMissile("FreezeBall",frandom(-6,6),1,0,0,0)
    TNT1 A 0 A_GunFlash("Flash3")
	TNT1 A 0 A_Playsound("misc/qdmg2",CHAN_5,1)
	FRSG A 2 Bright A_FireCustomMissile("FreezeBall",frandom(-6,6),1,0,0,0)
	Goto FireEnd
  Level2:
	TNT1 A 0 A_JumpIfInventory("PowerQuadDamage",1,"QuadLevel2")
    TNT1 A 0 A_GunFlash("Flash2")
	TNT1 A 0 A_FireCustomMissile("FreezeBall",frandom(-10,10),0)
	TNT1 A 0 A_FireCustomMissile("FreezeBall",frandom(-10,10),0)
	FRSG A 1 Bright A_FireCustomMissile("FreezeBall",frandom(-10,10))
    TNT1 A 0 A_GunFlash("Flash3")
	FRSG A 1 Bright
	Goto FireEnd
  QuadLevel2:
    TNT1 A 0 A_Playsound("misc/qdmg2",CHAN_5,1)
	Goto Level2+1
  Flash:
	FRSG A 12 Bright
	FRSF AB 3 Bright
	Goto LightDone
  Flash2:
    FRSF A 4 Bright A_Light1
    Goto LightDone
  Flash3:
	FRSF B 4 Bright A_Light1
    Goto LightDone
  Flash4:
	FRSG B 5 Bright A_Light1
	Goto LightDone
  Spawn:
    FRAS A -1 Bright
    Stop
  }
}

// Freezethrower Snowball

ACTOR FreezeBall : FastProjectile
{
  Radius 13
  Height 8
  Speed 80
  Damage (50*random(1,6))
  Projectile
  ProjectileKickBack 0
  RenderStyle Add
  Alpha 0.75
  SeeSound "weapons/freezeshot"
  DeathSound "weapons/freezehit"
  Obituary "%o got frozen solid by %k's Freezethrower."
  DamageType Ice
  Decal FreezeBlastMelt
  +RANDOMIZE
  States
  {
  Spawn:
    FBLS A 1 bright A_SpawnItem("FreezeBallTrail")
    loop
  Death:
	TNT1 A 0
    FBLS BCDE 4 bright
    stop
  }
}

ACTOR ExtremeFreezeBall : FreezeBall
{
	+EXTREMEDEATH
	States
	{
	XDeath:
	  TNT1 A 0 A_Playsound("misc/crit",CHAN_AUTO)
	  Goto Death
	}
}

ACTOR FreezeBallTrail
{
  Radius 13
  Height 8
  Projectile
  +RANDOMIZE
  +CLIENTSIDEONLY
  RenderStyle Add
  Alpha 0.75
  SeeSound "weapons/iceshot"
  DeathSound "weapons/icehit"
  Obituary "%o got frozen solid by %k's Freezethrower."
  States
  {
  Spawn:
    FRTR ABC 6 bright A_FadeOut(0.2)
    loop
  Death:
    FRTR ABC 4 bright
    stop
  }
}