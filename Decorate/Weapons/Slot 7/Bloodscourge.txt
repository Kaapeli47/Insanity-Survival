// Bloodscourge (Hexen)

ACTOR IDMBloodScourge : MWeapBloodscourge 10040
{
  Weapon.SelectionOrder 50
  Weapon.Sisterweapon "BloodscourgeLevel2"
  Weapon.AmmoType1 "IDMMana"
  Weapon.AmmoType2 "IDMMana"
  Weapon.AmmoUse1 10
  Weapon.AmmoUse2 0
  Weapon.AmmoGive1 200
  Weapon.AmmoGive2 0
  Weapon.KickBack 150
  Weapon.YAdjust 0
  Inventory.PickupMessage "Bloodscourge! (Slot 7)"
  Inventory.PickupSound "misc/w_pkup"
  Tag "Bloodscourge"
  //$Category Weapons
  +INVENTORY.INTERHUBSTRIP
  -WEAPON.ALLOW_WITH_RESPAWN_INVUL
  +WEAPON.NOLMS
  +WEAPON.BFG
  -INVENTORY.NOATTENPICKUPSOUND
  States
  {
  Spawn:
    WMBS A -1 Bright
    Stop
  Select:
	TNT1 A 0 A_PlaySound("weapons/wswitch",CHAN_WEAPON)
  Sloop:
    MSTF A 1 A_Raise
    Loop
  Deselect:
    MSTF A 1 A_Lower
    Loop
  Ready:
    MSTF AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFF 1 A_WeaponReady
    Loop
  Fire:
    MSTF G 4 Bright Offset(0,40)
	TNT1 A 0 A_Playsound("MageStaffFire",CHAN_WEAPON)
	TNT1 A 0 A_JumpIfInventory("PowerQuadDamage",1,"Quad")
	TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",-10,0)
	TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",-5,0)
	TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",5,0)
	TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",10,0)
    MSTF H 4 Bright Offset(0,48) A_FireCustomMissile("IDMMageStaffFX2")
    MSTF HII 2 Bright Offset(0,48)
    MSTF I 1 Bright Offset(0,40)
    MSTF J 5 Bright Offset(0,36)
    Goto Ready
  Quad:
    TNT1 A 0 A_Playsound("misc/qdmg",CHAN_AUTO,1)
	Goto Fire+2
  }
}

ACTOR BloodscourgeLevel2 : IDMBloodscourge
{
  Weapon.Sisterweapon "IDMBloodscourge"
  Weapon.AmmoUse1 10
  +POWERED_UP
  States
  {
  Fire:
    MSTF G 4 Bright Offset(0,40)
	TNT1 A 0 A_Playsound("MageStaffFire",CHAN_WEAPON)
	TNT1 A 0 A_JumpIfInventory("PowerQuadDamage",1,"Quad")
    TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    MSTF H 2 Bright Offset(0,48) A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    MSTF H 2 Bright Offset(0,48) A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    MSTF H 2 Bright Offset(0,48) A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    MSTF H 2 Bright Offset(0,48) A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    MSTF H 2 Bright Offset(0,48) A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    TNT1 A 0 A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),0)
    MSTF H 2 Bright Offset(0,48) A_FireCustomMissile("IDMMageStaffFX2",frandom(-10,10),1)
    MSTF II 2 Bright Offset(0,48)
    MSTF I 1 Bright Offset(0,40) A_Refire
    MSTF J 5 Bright Offset(0,36)
	Goto Ready
  Quad:
    TNT1 A 0 A_Playsound("misc/qdmg",CHAN_AUTO,1)
	Goto Fire+3
  }
}

// Bloodscourge Flame Ball

ACTOR IDMMageStaffFX2 replaces MageStaffFX2
// Doesn't inherit from MageStaffFX2 because the missile goes through enemies for whatever reason...
{
  Speed 17
  Height 8
  Damage 96
  Projectile
  ProjectileKickBack 10
  +SEEKERMISSILE
  +SCREENSEEKER
  +EXTREMEDEATH
  Damagetype "Fire"
  DeathSound "MageStaffExplode"
  Obituary "%o was wiped off the face of the universe by %k's Bloodscourge."
  States
  {
  Spawn:
    MSP2 AABBCCDD 1 Bright A_SeekerMissile(2,10) //A_MStaffTrack
    Loop
  Death:
    MSP2 E 3 Bright A_SetTranslucent(1,1)
    MSP2 F 4 Bright A_Explode(320,192,0)
    MSP2 GH 4 Bright
    MSP2 I 4 Bright
    Stop
  }
}